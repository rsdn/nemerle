using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Utility;

namespace Nemerle.Imperative.GoTo
{
  macro GoToMacro(@label : PExpr)
  syntax ("goto", @label)
  {
    Implementation.DoTransformGoTo(Macros.ImplicitCTX(), @label)
  }

  macro LabelMacro(@label : PExpr)
  syntax ("label", @label)
  {
    Implementation.DoTransformLabel(Macros.ImplicitCTX(), @label)
  }

  module Implementation
  {
    private GetLabelMap(typer : Typer) : Hashtable[string, PExprLabel]
    {
      mutable labelMap = typer.CurrentMethodBuilder.UserData[typeof(PExprLabel)] :> Hashtable[string, PExprLabel];
      when (labelMap == null)
      {
        labelMap = Hashtable.[string, PExprLabel]();
        typer.CurrentMethodBuilder.UserData[typeof(PExprLabel)] = labelMap;
      }
      labelMap
    }

    private GetLabel(typer : Typer, name : string) : PExprLabel
    {
      def labelMap = GetLabelMap(typer);
      mutable @label;
      unless (labelMap.TryGetValue(name, out @label))
      {
        @label = PExprLabel(typer.Manager, name);
        labelMap[name] = @label;
      }
      @label
    }
    
    public DoTransformGoTo(typer : Typer, @label : PExpr) : PExpr
    {
      Macros.DefineCTX(typer);
      
      match (@label)
      {
        | <[ $(name : name) ]> => GetLabel(typer, name.Id).Goto()
        | _ => Message.Error(@label.Location, "Expected simple name."); <[ () ]>
      }
    }

    public DoTransformLabel(typer : Typer, @label : PExpr) : PExpr
    {
      Macros.DefineCTX(typer);
      //assert2(false);
      
      match (@label)
      {
        | <[ $(name : name) ]> => 
          GetLabel(typer, name.Id).Label(@label.Location)
        | _ => Message.Error(@label.Location, "Expected simple name."); <[ () ]>
      }
    }
  }
  
  public sealed class PExprLabel
  {
    private         _manager        : ManagerClass;
    private         _name           : string;
    private         _id             : int;
    private mutable _isLabelDefined : bool;

    public this(manager : ManagerClass, name : string)
    {
      _manager = manager;
      _name    = name;
      _id      = _manager.GetNewId(name);
    }

    public Goto()  : PExpr
    {
      PExpr.Typed(Location.Default, TExpr.Goto(_manager.InternalType.Void, _id, 1))
    }

    public Label(loc : Location) : PExpr
    {
      if (_isLabelDefined)
      {
        def message = $"Label '$_name' ($_id) multiply defined";
        //assert2(false, message);
        Message.Error(message);
        <[ () ]>
      }
      else
        try PExpr.Typed(Location.Default, TExpr.Label(_manager.InternalType.Void, _id, TExpr.DefaultValue(loc, _manager.InternalType.Void)))
        //try PExpr.Typed(loc, TExpr.Label(loc, _manager.InternalType.Void, _id, TExpr.Literal(loc, _manager.InternalType.Void, Literal.Void())))
        finally _isLabelDefined = true;
    }
  }
}
