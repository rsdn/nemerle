using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Text;
using System.Collections.Generic;
using System.Console;
using System.Linq;
using System.Globalization;

namespace Nemerle.Json
{
  [Record]
  public variant JValue
  {
    | String {value : string}
    | Number {value : double}
    | Bool {value : bool}
    | Null

    public ToString(sb : StringBuilder) : StringBuilder
    {
      match (this)
      {
        | String(value) => sb.Append('"').Escape(value).Append('"');
        | Number(value) => sb.Append(value.ToString(CultureInfo.CreateSpecificCulture("en-US")));
        | Bool(value) => sb.Append(if (value) "true" else "false");
        | Null => sb.Append("null");
      }
    }

    public override ToString() : string
    {
      ToString(StringBuilder()).ToString()
    }
  }

  [Record]
  public variant JObject
  {
    | Object { items : list[string*JObject]}
    | Array { items : list[JObject]}
    | Value
      {
        value : JValue;

        public this()
        {
          value = JValue.Null();
        }

        public this(number : double) {value = JValue.Number(number)}
        public this(str : string) {value = JValue.String(str)}
        public this(boolean : bool) {value = JValue.Bool(boolean)}
      }

    public static Null : JObject = JObject.Value();
    public static Convert(jobj : JObject) : JObject {jobj}
    public static Convert(number : double) : JObject {JObject.Value(number);}
    public static Convert(str : string) : JObject {JObject.Value(str);}
    public static Convert(boolean : bool) : JObject {JObject.Value(boolean);}

    public ToString(sb : StringBuilder) : StringBuilder
    {
      match (this)
      {
        | Object(items) =>
          _ = sb.Append("{");
          foreach ((name, value) in items)
          {
            _ = sb.Append($<#"$name":#>);
            _ = value.ToString(sb);
            _ = sb.Append(',');
          }

          when (items.Any()) //remove last comma
            sb.Length --;

          sb.Append("}");

        | Array(items) =>
          _ = sb.Append("[");
          foreach (value in items)
          {
            _ = value.ToString(sb);
            _ = sb.Append(',');
          }

          when (items.Any()) //remove last comma
            sb.Length --;

          sb.Append("]");
        | Value(value) =>
          value.ToString(sb);
      }
    }

    public override ToString() : string
    {
      ToString(StringBuilder()).ToString()
    }
  }
}
