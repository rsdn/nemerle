using System;
using System.Globalization;
using System.Globalization.UnicodeCategory;
using SCG = System.Collections.Generic;

using Nemerle.Collections;

namespace LRPEGCC
{
  public module UnicodeCategoryTable
  {
    private _nameToCategoryTable : Hashtable[string, UnicodeCategory] = Hashtable();
    private _categoryToNameTable : Hashtable[UnicodeCategory, string] = Hashtable();
    private _categoryToRangeSetTable : Hashtable[UnicodeCategory, RangeSet] = Hashtable();

    this()
    {
      def classes =
      [
        ("Lu", UppercaseLetter),
        ("Ll", LowercaseLetter),
        ("Lt", TitlecaseLetter),
        ("Lm", ModifierLetter),
        ("Lo", OtherLetter),
        ("Mn", NonSpacingMark),
        ("Mc", SpacingCombiningMark),
        ("Me", EnclosingMark),
        ("Nd", DecimalDigitNumber),
        ("Nl", LetterNumber),
        ("No", OtherNumber),
        ("Zs", SpaceSeparator),
        ("Zl", LineSeparator),
        ("Zp", ParagraphSeparator),
        ("Cc", Control),
        ("Cf", Format),
        ("Cs", Surrogate),
        ("Co", PrivateUse),
        ("Pc", ConnectorPunctuation),
        ("Pd", DashPunctuation),
        ("Ps", OpenPunctuation),
        ("Pe", ClosePunctuation),
        ("Pi", InitialQuotePunctuation),
        ("Pf", FinalQuotePunctuation),
        ("Po", OtherPunctuation),
        ("Sm", MathSymbol),
        ("Sc", CurrencySymbol),
        ("Sk", ModifierSymbol),
        ("So", OtherSymbol),
        ("Cn", OtherNotAssigned)
      ];

      def ranges = SCG.List();
      mutable curCat = char.GetUnicodeCategory(0 :> char);
      mutable start = 0;
      for (mutable i = 1; i <= char.MaxValue : int; ++i)
      {
        def cat = char.GetUnicodeCategory(i :> char);
        when (cat != curCat)
        {
          ranges.Add((curCat, RangeSet.Range(start :> char, (i - 1) :> char)));
          start = i;
          curCat = cat;
        }
      }
      ranges.Add((curCat, RangeSet.Range(start :> char, char.MaxValue)));
      ranges.Reverse();

      foreach((str, cat) in classes)
      {
        _nameToCategoryTable[str] = cat;
        _categoryToNameTable[cat] = str;
        _categoryToRangeSetTable[cat] = ranges
          .FilterLazy((c, _) => c == cat)
          .Fold(RangeSet(), ((_, range), set) => set.AddRange(range));
      }
    }

    public StrToUnicodeCategory(x : string) : option[UnicodeCategory]
    {
      match (_nameToCategoryTable.TryGetValue(x))
      {
        | (cat, true) => Some(cat)
        | _ => None()
      }
    }

    public UnicodeCategoryToStr(cat : UnicodeCategory) : string
    {
      match (_categoryToNameTable.TryGetValue(cat))
      {
        | (s, true) => s
        | _ => "[Unknown unicode class]"
      }
    }

    public UnicodeCategoryToRangeSet(cat : UnicodeCategory) : RangeSet
    {
      match (_categoryToRangeSetTable.TryGetValue(cat))
      {
        | (s, true) => s
        | _ => throw ArgumentException("Unknown unicode class.", "cat");
      }
    }
  }
}