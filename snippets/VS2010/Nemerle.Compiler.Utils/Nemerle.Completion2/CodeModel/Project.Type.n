using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;
using System.Runtime.CompilerServices;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Token;
using Nemerle.Compiler.Utils;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Utility;
using Nemerle.Imperative;

using SCG = System.Collections.Generic;

namespace Nemerle.Completion2
{
  public partial class Project
  {
    public static TopKeywords : NemerleCompletionResult { get { Engine.TopKeywords } }
    
    public FindMember(memberName : string, _forPartialName : bool) : option[IMember]
    {
      // VladD2 TODO: сейчас реализация ищет только тип по полному имени. Доработать алгоритм, так чтобы он искл любой IMember
      def fullName = memberName.SplitToList('.');
      def nsTree   = NamespaceTree;
      def typeInfo = nsTree.LookupType(fullName, -1);
      typeInfo.Map(x => x) // изменяем тип с option[TypeInfo] на option[IMember]
    }

    // ---------------------------------------------------------------------------------------------------------------

    public FindObjectEverywhere(
      typeDecl  : Decl.Type,
      fileIndex : int,
      line      : int,
      col       : int,
      unpackMemberRefs = true
    )
      : Location * object * object
    {
      def res = FindObject(typeDecl, fileIndex, line, col);
      when (res[1] != null || res[2] != null)
        return res;
    
      def checkType(pexpr, texpr : TypeVar) 
      {
        def pargs =
          match (pexpr)
          {
          | PExpr.Array  (_, p)          => [p]
          | PExpr.Call   (_, p)
          | PExpr.Indexer(_, p)
          | PExpr.GenericSpecifier(_, p) => p
          | _                            => []
          }

        match (pargs.Find((p) => p.Location.Contains(line, col)))
        {
        | Some(p) =>
            def targs =
              match (texpr)
              {
              | mType is FixedType =>
                match (mType)
                {
                | FixedType.Array (p, _) => [p]
                | FixedType.Fun   (f, t) => [f, t]
                | FixedType.Class (_, p)
                | FixedType.Tuple (p)    => p
                | _                  => []
                }
              | _                    => []
              };

            def idx = pargs.IndexOf(p);
            checkType(p, targs.Nth(idx))

        | _       => (pexpr.Location, null, texpr)
        }
      }
      def getMethod(method)
      {
        def ast = method.Ast;

        // Body must be processed first.
        if (ast.header.Location.Contains(line, col))
        {
          if (ast.header.ParsedSplicableName.Location.Contains(line, col))
            (ast.header.ParsedSplicableName.Location, ast, method)
          else if (ast.header.ParsedReturnType.Location.Contains(line, col))
            checkType(ast.header.ParsedReturnType, method.ReturnType);
          else if (ast.modifiers.custom_attrs.OrderedContains(line, col))
          {
            // PB06116: TODO: CustomAttributes
            //
            (Location.Default, null, null)
          }
          else
          {
            def getParam(pparms, tparms)
            {
              match (pparms)
              {
              | []     => (Location.Default, null, null)
              | p :: _ =>

                if (p.name.Location.Contains(line, col))
                  (p.name.Location, p.name, tparms.Head)
                else if (p.Type.Location.Contains(line, col))
                  checkType(p.Type, tparms.Head.ty)
                else
                  getParam(pparms.Tail, tparms.Tail)

              }
            }

            getParam(ast.header.ParsedParameters, method.Header.Parameters);
          }
        }
        else
        {
          def typarms = ast.header.ParsedTypeParameters;

          if (typarms.tyvars.OrderedContains(line, col))
            match (typarms.tyvars.Find(t => t.Location.Contains(line, col)))
            {
            | Some(t) => (t.Location, null, method.Header.TypeParameters.Nth(typarms.tyvars.IndexOf(t)))
            | _       => (Location.Default, null, null)
            }
          else
          {
            def res = 
            {
              def found = ExprFinder().FindEverywhere(method.BodyParsed, method.BodyTyped, line, col, unpackMemberRefs);
              def (loc, _, typedObject) = found;

              def getParameterNumber(name, parameters, i = 0)
              {
                | (_, parameter :: tail, _) =>
                  if (parameter.Name == name)
                    i
                  else
                    getParameterNumber(name, tail, i + 1)
                | (_, [], _) => -1
              }

              match (typedObject)
              {
                | reference is LocalValue when (reference.ValKind is LocalValue.Kind.FunParm) =>
                  def function = reference.DefinedIn;
                  def number = getParameterNumber(reference.Name, function.ParametersDeclarations);
                  if (number < 0)
                    found
                  else
                  {
                    def parsedParameter = function.ParametersDeclarations.Nth(number);
                    def typedParameter = function.Parameters.Nth(number);
                    (loc, parsedParameter.name, typedParameter)
                  }
                | ty is TExpr.This | ty is TExpr.Base => (loc, null, ty.FixedType())
                | _ => found
              }
            }
            
            when (res[1] != null || res[2] != null)
              return res;
              
            // Covarian/Contravariant constraints breaks OrderedContains optimization
            // so process them one-by-one.
            //
            def constraints = typarms.constraints.RevFilter(c => !Utils.IsSpecialConstraint(c.ty));

            match (constraints.Find(c => c.ty.Location.Contains(line, col)))
            {
            | Some(c) =>

              def tyVarConstraints = constraints.Filter(tc => tc.tyvar.Equals(c.tyvar));
              def typarm = method.Header.TypeParameters.Nth(typarms.tyvars.IndexOf(c.tyvar));

              Debug.Assert(tyVarConstraints.Length == typarm.Constraints.Length);
              (c.ty.Location, null, typarm.Constraints.Nth(tyVarConstraints.IndexOf(c)))

            | _       =>           (Location.Default, null, null)
            }
          }
        }
      }

      def typeBuilder = typeDecl.Ast.TypeBuilder;
      if (typeBuilder.IsDelegate) FindObject(typeDecl, fileIndex, line, col)
      else
      {
        def members = typeBuilder.GetMembers();
        
        foreach (member in members)
        {
          | mb is MethodBuilder   =>
           	when (mb.Name == "IndxEntry__StImpl") 
           	{
           	  assert2(true);
           	}
            def res = getMethod(mb);
            when (res[1] != null || res[2] != null)
              return res;
              
          | _ => ()
        }
        
        (Location.Default, null, null)
      }
    }

    /// Finds an object by provided location.
    /// Returns object location * associated Parsetree * associated Typedtree
    private FindObject(
      typeDecl  : Decl.Type,
      fileIndex : int,
      line      : int,
      col       : int,
      unpackMemberRefs = true
    )
      : Location * object * object
    {
      when (typeDecl.Ast != null && typeDecl.Ast.TypeBuilder == null)
        return (typeDecl.Ast.Location, null, typeDecl.Ast);

      def typeBuilder = typeDecl.Ast.TypeBuilder;
      def member      = 
        if (typeBuilder.IsDelegate) typeBuilder : IMember
        else typeBuilder.GetMemberByLocation(fileIndex, line, col) ?? typeBuilder;

      def checkType(pexpr, texpr : TypeVar) 
      {
        def pargs =
          match (pexpr)
          {
            | PExpr.Array  (_, p)          => [p]
            | PExpr.Call   (_, p)
            | PExpr.Indexer(_, p)
            | PExpr.GenericSpecifier(_, p) => p
            | _                            => []
          };

        match (pargs.Find((p) => p.Location.Contains(line, col)))
        {
          | Some(p) =>
              def targs =
                match (texpr)
                {
                  | mType is FixedType =>
                    match (mType)
                    {
                    | FixedType.Array (p, _) => [p]
                    | FixedType.Fun   (f, t) => [f, t]
                    | FixedType.Class (_, p)
                    | FixedType.Tuple (p)    => p
                    | _                  => []
                    }
                  | _                    => []
                };

              def idx = pargs.IndexOf(p);
              checkType(p, targs.Nth(idx))

          | _       => (pexpr.Location, null, texpr)
        }
      }

      def getMethod(method)
      {
        def ast = method.Ast;
        def headerLocation = ast.header.Location;
        def bodyLocation = method.BodyLocation;

        // Body must be processed first.
        
        if (method.IsBodyCompilable 
            && (bodyLocation.Contains(line, col) || bodyLocation.IsGenerated))
        {
          def getParameterNumber(name, parameters, i = 0)
          {
            | (_, parameter :: tail, _) =>
              if (parameter.Name == name)
                i
              else
                getParameterNumber(name, tail, i + 1)
            | (_, [], _) => -1
          }

          def (loc, _, typedObject) as found = ExprFinder().Find(method.BodyParsed, method.BodyTyped, line, col, unpackMemberRefs);

          match (typedObject)
          {
            | reference is LocalValue when (reference.ValKind is LocalValue.Kind.FunParm) =>
              def function = reference.DefinedIn;
              def number = getParameterNumber(reference.Name, function.ParametersDeclarations);
              if (number < 0)
                found
              else
              {
                def parsedParameter = function.ParametersDeclarations.Nth(number);
                def typedParameter = function.Parameters.Nth(number);
                (loc, parsedParameter.name, typedParameter)
              }
            | ty is TExpr.This | ty is TExpr.Base => (loc, null, ty.FixedType())
            | _ => found
          }
        }
        else if (headerLocation.Contains(line, col) || headerLocation.IsGenerated)
        {
          if (ast.header.ParsedSplicableName.Location.Contains(line, col))
            (ast.header.ParsedSplicableName.Location, ast, method)
          else if (ast.header.ParsedReturnType.Location.Contains(line, col))
            checkType(ast.header.ParsedReturnType, method.ReturnType);
          else if (ast.modifiers.custom_attrs.OrderedContains(line, col))
          {
            // PB06116: TODO: CustomAttributes
            //
            (Location.Default, null, null)
          }
          else
          {
            def getParam(pparms, tparms)
            {
              match (pparms)
              {
              | []     => (Location.Default, null, null)
              | p :: _ =>

                if (p.name.Location.Contains(line, col))
                  (p.name.Location, p.name, tparms.Head)
                else if (p.Type.Location.Contains(line, col))
                  checkType(p.Type, tparms.Head.ty)
                else
                  getParam(pparms.Tail, tparms.Tail)

              }
            }

            getParam(ast.header.ParsedParameters, method.Header.Parameters);
          }
        }
        else
        {
          def typarms = ast.header.ParsedTypeParameters;

          if (typarms.tyvars.OrderedContains(line, col))
            match (typarms.tyvars.Find(t => t.Location.Contains(line, col)))
            {
            | Some(t) => (t.Location, null, method.Header.TypeParameters.Nth(typarms.tyvars.IndexOf(t)))
            | _       => (Location.Default, null, null)
            }
          else
          {
            // Covarian/Contravariant constraints breaks OrderedContains optimization
            // so process them one-by-one.
            //
            def constraints = typarms.constraints.RevFilter(c => !Utils.IsSpecialConstraint(c.ty));

            match (constraints.Find(c => c.ty.Location.Contains(line, col)))
            {
            | Some(c) =>

              def tyVarConstraints = constraints.Filter(tc => tc.tyvar.Equals(c.tyvar));
              def typarm = method.Header.TypeParameters.Nth(typarms.tyvars.IndexOf(c.tyvar));

              Debug.Assert(tyVarConstraints.Length == typarm.Constraints.Length);
              (c.ty.Location, null, typarm.Constraints.Nth(tyVarConstraints.IndexOf(c)))

            | _       =>           (Location.Default, null, null)
            }
          }
        }
      }

      def checkMethods (methods)
      {
        | hd :: tail =>
          if (hd != null && hd.BodyLocation.Contains(line, col))
            getMethod(hd)
          else
            checkMethods(tail)
        | []         => (Location.Default, null, null)
      }

      def processMember(member : IMember)
      {
        | mb is MethodBuilder   => getMethod(mb)
        | eb is EventBuilder    => 

            if (eb.Ast.ty.Location.Contains(line, col))
              checkType(eb.Ast.ty, eb.GetMemType())
            else if (eb.Ast.name.Location.Contains(line, col))
              (eb.Ast.name.Location, null, eb)
            else
              checkMethods([eb.GetAdder(), eb.GetRemover()])

        | fb is FieldBuilder    => 

          if (typeBuilder.IsEnum)
            if (fb.Attributes %&& NemerleModifiers.SpecialName)
              (Location.Default,     null, null)
            else
              (fb.Location,          null, fb)
          else
            if (fb.Ast.ty.Location.Contains(line, col))
              checkType(fb.Ast.ty, fb.GetMemType())
            else if (fb.Ast.name.Location.Contains(line, col))
              (fb.Ast.name.Location, null, fb)
            else if (fb.InitializerLocation.Contains(line, col))
              getMethod(fb.LookupInitializerMethod())
            else
              (fb.Location,          null, fb)

        | pb is PropertyBuilder => 

            if (pb.Ast.returnType.Location.Contains(line, col))
              match (pb.GetMemType())
              {
                | f is FixedType.Fun => checkType(pb.Ast.returnType, f.to) // Indexer
                | t                  => checkType(pb.Ast.returnType, t)    // Regular prop
              }
            else if (pb.Ast.name.Location.Contains(line, col))
              (pb.Ast.name.Location, null, pb)
            else
            {
              def parms = pb.Ast.parameters;

              match (parms.Find(p => p.Type.Location.Contains(line, col)))
              {
              | Some(p) => checkType(p.Type, pb.GetParameters().Nth(parms.IndexOf(p)))
              | _       => checkMethods([pb.GetGetter() :> MethodBuilder, pb.GetSetter() :> MethodBuilder])
              }
            }

        | tb is TypeBuilder     => 

          def lookupType(p)
          {
          | PExpr.Ref (name) =>
              match (name.context.LookupType([name.Id], tb, -1))
              {
              | Some(ty) => (p.Location, null, ty)
              | _        => (Location.Default, null, null)
              }
          | PExpr.Indexer(obj, args) =>
              match ((obj :: args).Find(a => a.Location.Contains(line, col)))
              {
              | Some (p) => lookupType(p)
              | _        => (Location.Default, null, null)
              }
          | _        => (p.Location, null, null)
          }

          def processAst (tb, ast)
          {
            match (ast)
            {
            | TopDeclaration.Class    (extends, _)
            | TopDeclaration.Variant  (extends, _)
            | TopDeclaration.Interface(extends, _) =>

              // Remove compiler generated base type or interface
              //
              def extends = extends.Filter(cls => !cls.Location.IsGenerated);

              def typarms = ast.typarms;

              if (extends.OrderedContains(line, col))
                match (extends.Find(t => t.Location.Contains(line, col)))
                {
                | Some(p) => (p.Location, p, p.TypedObject)
                | _       => (Location.Default, null, null)
                }
              else if (typarms.tyvars.OrderedContains(line, col))
                match (typarms.tyvars.Find(t => t.Location.Contains(line, col)))
                {
                | Some(t) => (t.Location, null, tb.Typarms.Nth(typarms.tyvars.IndexOf(t)))
                | _       => (Location.Default, null, null)
                }
              else
              {
                // Covariant/Contravariant constraints breaks OrderedContains optimization
                // so process them one-by-one.
                //
                match (typarms.constraints.Find(c => c.ty.Location.Contains(line, col)))
                {
                | Some (c) => lookupType(c.ty)
                | _        => (ast.PName.Location, null, tb : object)
                }
              }
            | _ => (ast.PName.Location, null, tb : object)
            }
          }

          match (tb.AstParts.Find(part => part.Location.Contains(fileIndex, line, col)))
          {
          | Some(ast) => processAst (tb, ast)
          | _         => (Location.Default, null, null)
          }

        | null                  => (Location.Default, null, null)
        | _                     => 
          Debug.Assert(false, $"Unknown member type '$member'.");
          throw System.Exception($"Unknown member type '$member'.");
      }
      
      def result = processMember(member);
      match (member, result)
      {
        | (mb is MemberBuilder, (_, null, null)) => mb.CallForAllRelated(processMember)
        | _ => 
          match (result)
          {
            //HACK: VladD2: The compiler not set information for static type ref in TExpr.StaticRef.
            | (_, PExpr.Member(pObj, _), TExpr.StaticRef(tObj, _, _)) 
              when pObj.Location.Contains(line, col) => 
              def tryFindNestedType(pObj, tObj : FixedType)
              {
                | (PExpr.Member(pObj, _), FixedType.Class(ti, tyArgs)) 
                  when ti.DeclaringType != null && pObj.Location.Contains(line, col) => 
                  tryFindNestedType(pObj, FixedType.Class(ti.DeclaringType, tyArgs))
                | _ => (pObj.Location, pObj, tObj)
              }
              
              tryFindNestedType(pObj, tObj)
              
            | _ => result
          }
      }
    }

    private FindPExprs(typeDecl  : Decl.Type, loc : Location) : list[PExpr]
    {
      def typeBuilder = typeDecl.Ast.TypeBuilder;
      
      when (typeBuilder.IsDelegate)
        return [];
        
      def member = typeBuilder.GetMemberByLocation(loc.FileIndex, loc.Line, loc.Column);
      def tryMethod(method : IMethod) : MethodBuilder
      {
        | m is MethodBuilder when m.BodyLocation.Contains(loc) => m
        | null | _ => null
      }
      def getMethod(member : IMember) : MethodBuilder
      {
        | mb is MethodBuilder   => mb
        | eb is EventBuilder    => tryMethod(eb.GetAdder()) ?? tryMethod(eb.GetRemover())
        | fb is FieldBuilder    => tryMethod(fb.LookupInitializerMethod())
        | pb is PropertyBuilder => tryMethod(pb.GetGetter()) ?? tryMethod(pb.GetSetter())
        | _tb is TypeBuilder    => assert2(false); null
        | _                     => null
      }
      def method  = getMethod(member);
      
      when (method == null || !method.BodyLocation.Contains(loc) && method.IsBodyCompilable)
        return [];
      
      method.EnsureCompiled();
      def exprs = SCG.HashSet();
      def walker = ExprWalker();
      def visit(info : ExprWalkInfo)
      {
        match (info.Node)
        {
          | expr is PExpr =>
            def find(expr)
            {
              when (expr.TypedObject is TExpr.MacroEnvelope(expandedPExpr = subExpr))
                walker.Walk(subExpr, visit);
              when (loc.Contains(expr.Location) && !expr.Location.IsGenerated)
                _ = exprs.Add(expr);
            }
            
            find(expr)
            
          | _ => ()
        }        
      }
      walker.Walk(method.BodyParsed, visit);
        
      def res = exprs.NToList();
      res
    }

    private GetTypeQuickTip(
      typeDecl  : Decl.Type,
      fileIndex : int,
      line      : int,
      col       : int,
      //2IT: практически любой класс в компиляторе содержит ссылку на ManagerClass.
      // По крайней мере MemberBuilder и TypeBuilder точно их имеют.
      // Так что этот параметр скорее всего ошибка.
      manager   : ManagerClass
    )
      : QuickTipInfo
    {
      def (loc, pObj, tObj) = FindObject(typeDecl, fileIndex, line, col, unpackMemberRefs = false);

      match (tObj)
      {
      | me is TExpr.MacroEnvelope         => (QuickTipInfo(loc, me))
      | lv is LocalValue                  => (QuickTipInfo(loc, lv))
      | mm is IMember                     => (QuickTipInfo(loc, mm, manager))
      | st is StaticTypeVar                 => (QuickTipInfo(loc, st))
      | tv is TypeVar                       => (QuickTipInfo(loc, tv))
      | tc is TExpr.ImplicitValueTypeCtor => (QuickTipInfo(loc, tc.ty))
      | fh is Typedtree.TFunHeader        => (QuickTipInfo(loc, fh))
      | fp is Typedtree.TParameter          => (QuickTipInfo(loc, fp))
      | p  is Pattern.Enum                => (QuickTipInfo(loc, p.fld, manager))
      | dv is TExpr.DefValIn when dv.name.Location.Contains(fileIndex, line, col)
                                          => QuickTipInfo(loc, dv.name)
      | TExpr.Call(func=TExpr.MethodRef(meth=IMethod(Name=name) as accessor)) 
                                          when name.StartsWith("get_") || name.StartsWith("set_")
                                          => (QuickTipInfo(loc, accessor, manager))
      | null                              => (null)
      | expr is TExpr                     => QuickTipInfo(loc, pObj, expr, manager)
#if DEBUG
      | o                                 => (QuickTipInfo(loc, o))
#else
      | _                                 => (null)
#endif
      }
    }

    GetMethodTip(typeDecl : Decl.Type, fileIndex : int, line : int, col : int, source : IIdeSource) : MethodTipInfo
    {
      def typeBuilder = typeDecl.Ast.TypeBuilder;
      def member      = typeBuilder.GetMemberByLocation(fileIndex, line, col);

      match (member)
      {
        | field  is FieldBuilder    => GetMethodTip(field.LookupInitializerMethod(), line, col, source)
        | method is MethodBuilder   => GetMethodTip(method, line, col, source)
        | prop   is PropertyBuilder => 
          def getMethodTip(prop : IMethod) : MethodTipInfo
          {
            | accessor is MethodBuilder when accessor.BodyInnerLocation.Contains(fileIndex, line, col) => 
              GetMethodTip(accessor, line, col, source)
              
            | _ => null
          }
          
          getMethodTip(prop.Getter) ?? getMethodTip(prop.Setter)
          
        | null                      => null
        | _                         => throw System.Exception($"Unknown member type '$member'.");
      }
    }

    [RecordIgnore] mutable _previousMethodHintData : option[int * int * Location * CompletionResult] = None();

    GetMethodTip(method : MethodBuilder, line : int, col : int, source : IIdeSource) : MethodTipInfo
    {
      def loc = method.BodyLocation;
      
      if (loc.Contains(line, col)) // completion in method body
      {
        def timer = Stopwatch.StartNew();
        source.LockWrite();
        def (fileIndex, version, bodyCode) = 
        try
        {
          (source.FileIndex,
          source.CurrentVersion,
          source.GetRegion(loc.Line, loc.Column, loc.EndLine, loc.EndColumn))
        } finally { source.UnlockWrite(); };
        
        def finder   = TokenFinder();
        def tokens   = finder.Find(method.BodyTokens, line, col);

        Debug.WriteLine($"Comlete took $(timer.Elapsed)");
        def findMethodNameToken(tokens) : option[_]
        {
          def tokenToListRev(token, acc = [])
          {
            if (token == null) acc
            else tokenToListRev(token.Next, token :: acc)
          }
          retBlock :
          {
            foreach (token in tokens)
            {
              | LooseGroup(children) =>
                def tokenList = tokenToListRev(children);
                def loop (tokenList)
                {
                  | (RoundGroup as parms) :: ((RoundGroup(LooseGroup(Identifier as id)) :: _) as tail)
                  | (RoundGroup as parms) :: ((RoundGroup(Identifier as id) :: _) as tail)
                  | (RoundGroup as parms) :: ((Identifier as id) :: tail)
                  | (RoundGroup as parms) :: ((x :: _) as tail) with id = Identifier(x.Location, "") => 
                      def loc = parms.Location;
                      
                      if (loc.Contains(line, col) && !(loc.Line == line && loc.Column == col || loc.EndLine == line && loc.EndColumn == col))
                        retBlock(Some(id, parms));
                      else
                        loop(tail); // we outside parameters list...

                  | _ :: tail => loop(tail)
                  | _ => ()
                }

                loop(tokenList);
              | _ => ()
            }

            None()
          }
        }

        match (findMethodNameToken(tokens))
        {
          | Some((id, paramsGroup)) =>
            def name = id.name;
            def locName = id.Location;
            
            def result = if (_previousMethodHintData.IsSome)
              {
                def (prevFileIndex, prevVer, prevLoc, prevRes) = _previousMethodHintData.UnSome();
                
                if (prevFileIndex == fileIndex && prevVer == version && prevLoc == paramsGroup.Location)
                  prevRes
                else
                  EngineEx.RunCompletionEngine(method, bodyCode, loc, locName.EndLine, locName.EndColumn);
              }
              else EngineEx.RunCompletionEngine(method, bodyCode, loc, locName.EndLine, locName.EndColumn);

            if (result != null)
            {
              def overloads = List();

              def cmpName(m : IMethod)
              {
                if (name == "")
                  true
                else if (m.Name == ".ctor" || m.Name == "..ctor")
                  m.DeclaringType.Name == name
                else
                  m.Name == name
              }

              mutable constant_object_type = None();
              
              foreach (elem in result.Elems)
              {
                | Overloads(values) =>
                  foreach (overload in values)
                    match (overload.Member)
                    {
                      | m is IMethod when cmpName(m) => overloads.Add(overload);
                      | fld is IField when fld.Name == "_N_constant_object" => constant_object_type = Some(overload.From);
                      | _ => ()
                    }
                
                | Local(func) when func.Name == name => 
                  def tip = LocalFuncMethodTipInfo(func, id, paramsGroup);
                  return if (tip.HasTip) tip else null;
                  
                | _ => ()
              }

              if (overloads.Count > 0)
              {
                overloads.Sort((x, y) =>
                  {
                    def xparms = (x.Member :> IMethod).GetParameters();
                    def yparms = (y.Member :> IMethod).GetParameters();

                    xparms.Length - yparms.Length;
                  });
                  
                _previousMethodHintData = Some(fileIndex, version, paramsGroup.Location, result);
                
                def calcActiveParam(param, index = 0)
                {
                  if (param == null) index
                  else 
                  {
                    def pEndLine = param.Location.EndLine;
                    def pEndCol  = param.Location.EndColumn;
                    if (line < pEndLine || line == pEndLine && col <= pEndCol)
                      index
                    else
                      calcActiveParam(param.Next, index + 1)
                  }
                }
                def paramsCount(param, count = 0)
                {
                  if (param == null) count
                  else paramsCount(param.Next, count + 1)
                }

                def paramsCnt   = paramsCount(paramsGroup.Child);
                def activeParam = calcActiveParam(paramsGroup.Child);
                def overloads   = overloads.Distinct(OverloadsEqualityComparer()).ToList();
                def res         = overloads.FindAll(o => (o.Member :> IMethod).GetParameters().Length == paramsCnt);
                
                def defaultMethodIndex = 
                  if (res.Count == 1)
                      overloads.FindIndex(o => (o.Member :> IMethod).GetParameters().Length == paramsCnt)
                  else
                  {
                    method.EnsureCompiled();
                    def (eLoc, pExpr, tExpr) = ExprFinder().FindExact(method.BodyParsed, method.BodyTyped, locName, true);
                    def index = overloads.FindIndex(o => o.Member == tExpr);
                    _ = eLoc; _ = pExpr; _ = tExpr; _ = activeParam;
                    index
                  };
                
                def tip = OverloadsMethodTipInfo(overloads, id, paramsGroup, defaultMethodIndex, activeParam);
                if (tip.HasTip) tip else null
              }
              else if (constant_object_type.IsSome)
                VariantConstantObjectTipInfo(constant_object_type.UnSome(), id, paramsGroup)
              else
                null
            }
            else null

          | None => null
        }
      }
      else
      {
        if (method.Ast.header.Location.Contains(line, col))
        {
          (null)
        }
        else
          (null)
      }
    }
  }
}
