using System;
using System.Diagnostics;
using System.Collections.Generic;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Completion2;
using Nemerle.Compiler;
using Nemerle.Completion2.CodeFormatting;
using Nemerle.Utility;

namespace Nemerle.Completion2
{
  // This class encapsulates work with token stream. It supports 
  // insertion and removal of single tokens or token groups, and it 
  // also does transparent relocation of tokens affected by any operation.
  // And the last, but not least: it can build a diff between original stream and final one.
  // NOTE: honestly, not all features are implemented yet. :-)
  public class TokenStreamHandler
  {
    private class PositionHolder
    {
      mutable _tokens : List[Token] = null;
      mutable _currentIndex = 0;
      mutable isInitialized = false;
      
      //public this(){}
      
      // We're assuming that tokens are ordered as they appear in the source, i.e. no reversing and shuffling.
      public this(tokens : List[Token], currentIndex : int)
      {
        Initialize(tokens, currentIndex);
      }
      
      public Initialize(tokens : List[Token], currentIndex : int) : void
      {
        isInitialized = true;
        
        _tokens = tokens;
        _currentIndex = currentIndex;
      } 
      
      AlterPosition(advance : bool) : void
      {
        if(advance)
          _currentIndex++;
        else
          _currentIndex--;
      }
      
      GetCurrentToken() : Token
      {
        if(_currentIndex < 0 || _currentIndex >= _tokens.Count)
          null
        else
          _tokens[_currentIndex];
      } 
      
      public GetNextToken() : Token
      {
        def tok = GetCurrentToken();
        AlterPosition(true);
        tok
      }
      
      public GetPrevToken() : Token
      {
        def tok = GetCurrentToken();
        AlterPosition(false);
        tok
      }
      
    }
  
    mutable _originalStream : List[Token] = null;
    mutable _stream         : List[Token] = null;
    mutable _isInitialized  : bool        = false;
    
    public this() { }
    
    public this(tokens : array[Token])
    {
      Initialize(tokens);
    }
    
    public Initialize(tokens : array[Token]) : void
    {
      _isInitialized = true;
      _originalStream = List(tokens);
      _stream = List(tokens);
    }

    RelocateToken(tok : Token, line : int, col : int, lineDelta : int, colDelta : int) : void
    {
      match(tok)
      {
        | t when t.Location.Line == line && t.Location.Column > col with loc = t.Location =>
            t.Location = Location(loc.FileIndex,
                                    loc.Line + lineDelta, 
                                    loc.Column + colDelta,
                                    loc.EndLine,
                                    loc.EndColumn);
                                
        | t when t.Location.Line > line with loc = t.Location =>
            t.Location = Location(loc.FileIndex,
                                    loc.Line + lineDelta, 
                                    loc.Column,
                                    loc.EndLine,
                                    loc.EndColumn);
        | _ => ()
      }
    }
    
    Relocate(index : int, lineDelta : int, colDelta : int) : void
    {
      def loc = _stream[index].Location;
      for(mutable i = index; i < _stream.Count; i++)
      {
        RelocateToken(_stream[i], loc.Line, loc.Column, lineDelta, colDelta); 
      }
    }
    
    SetStartPosition(line : int, col : int) : int
    {
      mutable result = 0;
      
      // Here we rely on the fact that all tokens are in the right order.
      foreach (tok in _stream)
      {
        if(tok.Location.Contains(line, col) && tok.Location.EndColumn != col)
          Nemerle.Imperative.Return(result);
        else
          result++;
      }
      result
    }
    
    public InsertBefore(token : Token, insertion : Token) : void
    {
      def idx = _stream.FindIndex(token : object == _);
      Debug.WriteLine($"Insertion: found token $token at ($(token.Location.Line), $(token.Location.Column))");
      _stream.Insert(idx, insertion);
      def loc = insertion.Location;
      Relocate(idx, loc.EndLine - loc.Line, loc.EndColumn - loc.Column); 
    }
    
    // Removes tokens from startFrom token and up to (but not including) upTo token.
    public Remove(startFrom : Token, upTo : Token) : void
    {
      def startIndex = _stream.FindIndex(startFrom : object == _);
      def endIndex = _stream.FindIndex(upTo : object == _);
      assert(startIndex < endIndex);
      
      mutable removedLocation = Location.Default;
      for(mutable i = startIndex; i < endIndex; i++)
        removedLocation = _stream[i].Location.Combine(removedLocation);
      
      _stream.RemoveRange(startIndex, endIndex - startIndex);
      Relocate(startIndex,  
                 removedLocation.EndLine - removedLocation.Line,
                 removedLocation.EndColumn - removedLocation.Column); 
      
    }
    
    public FindAll(predicate : Token -> bool) : IEnumerable[Token]
    {
      _stream.Filter(predicate);
    }
        
    Find(line : int,col : int,predicate : Token -> bool,finder : PositionHolder -> Token) : Token
    {
      def startIndex = SetStartPosition(line, col);
      def holder = PositionHolder(_stream, startIndex);
      
      mutable result = holder.GetNextToken();
      while(result != null && !predicate(result))
        result = finder(holder);//holder.GetNextToken();
        
      when(result == null)
        throw FormatterException($"Token not found (next from ($line, $col), using predicate $predicate)");
      result 
    }
    
    FindRange(line : int,col : int,predicate : Token -> bool,finder : PositionHolder -> Token) : array[Token]
    {
      def startIndex = SetStartPosition(line, col);
      def holder = PositionHolder(_stream, startIndex);
      
      def loop(lst)
      {
        def tok=finder(holder);
        when (tok == null) throw FormatterException($"Token not found (next from ($line, $col), using predicate $predicate)");
        def res= tok :: lst;
        if (predicate(tok)) res; else loop(res);
      }
      def res=loop([]);
      res.Rev().ToArray();
    }
    
    public FindNextIf(line : int, col : int, predicate : Token -> bool) : Token
    {
       Find(line,col,predicate,_.GetNextToken());
    }

    public TryFindNextIf(line : int, col : int, predicate : Token -> bool) : option[Token]
    {
      def startIndex = SetStartPosition(line, col);
      def holder = PositionHolder(_stream, startIndex);
      
      mutable result = holder.GetNextToken();
      while(result != null && !predicate(result))
        result = holder.GetNextToken();
        
      if (result == null) None() else Some(result)
    }
    
    public FindPrevIf(line : int, col : int, predicate : Token -> bool) : Token
    {
       Find(line,col,predicate,_.GetPrevToken());
    }
    
    public FindNextRange(line : int, col : int, predicate : Token -> bool) : array[Token]
    {
        FindRange(line,col,predicate,_.GetNextToken());
    }
    
    public FindPrevRange(line : int, col : int, predicate : Token -> bool) : array[Token]
    {
        FindRange(line,col,predicate,_.GetPrevToken());      
    }
    
    public FindAt(line : int, col : int) : Token
    {
      def startIndex = SetStartPosition(line, col);
      _stream[startIndex];
    }
    
    public Diff() : List[FormatterResult]
    {
      throw NotImplementedException("Diff");
    }

    
  }
}
