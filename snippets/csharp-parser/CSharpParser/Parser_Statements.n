using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Peg;

namespace CSharpParser
{
  public partial class Parser
  {
    #region Empty statement

    //emptyStatement : Statement = ";"s;
    emptyStatement(_semicolon : NToken) : Statement
    {
      Statement.Empty()
    }

    #endregion

    #region Statement with label

    //labeledStatement : Statement = identifier ":"s statement;
    labeledStatement(label : Identifier, _colon : NToken, stmt : Statement) : Statement
    {
      Statement.Label(label, stmt)
    }

    #endregion

    #region Declaration statements

    //localConstantDeclaration : Statement = "const"S anyType constantDeclarators;
    localConstantDeclaration( _const        : NToken,
                              constantType  : Expr,
                              decls         : list[ConstantDeclarator]) : Statement
    {
      Statement.ConstantDecl(constantType, decls.Map(d => (d.Location, d.Value[0], d.Value[1])))
    }

    //localVariableType_var : Expr = "var"S;
    localVariableType_var(_var : NToken) : option[Expr]
    {
      None()
    }

    //localVariableType_any : Expr = anyType;
    localVariableType_any(t : Expr) : option[Expr]
    {
      Some(t)
    }

    //localVariableDeclaration : Statement = localVariableType variableDeclarators;
    localVariableDeclaration( variableType  : option[Expr],
                              decls         : list[VariableDeclarator]) : Statement
    {
      Statement.VariableDecl(variableType.WithDefault(null), decls.Map(d => (d.Location, d.Value[0], d.Value[1])))
    }

    //declarationStatement : Statement = (localConstantDeclaration / localVariableDeclaration) ";" s;
    declarationStatement(opt : Statement, _semicolon : NToken) : Statement
    {
      opt
    }

    #endregion

    #region Expression statements

    //statementExpressionList : ExprList  = statementExpression (","s statementExpression)*;
    statementExpressionList(head : Expr,
                            tail : SCG.List[NToken * Expr]) : list[Expr]
    {
      head.Cons(tail)
    }

    //expressionStatement     : Statement = statementExpression ";"s;
    expressionStatement(expr : Expr, _semicolon : NToken) : Statement
    {
      Statement.Expression(expr)
    }

    #endregion

    #region Selection statements

    //ifStatement : Statement = "if"S "("s booleanExpression ")"s embeddedStatement ("else"S embeddedStatement)?;
    ifStatement(_if       : NToken,
                _lparen   : NToken,
                expr      : Expr,
                _rparen   : NToken,
                trueStmt  : Statement,
                falseStmt : option[NToken * Statement]) : Statement
    {
      def falseStmt = match(falseStmt) {
        | Some( (_, stmt) ) => stmt
        | _ => null
      }
      Statement.If(expr, trueStmt, falseStmt)
    }

    //switchLabel_case    : Expr = "case"S constantExpression ":"s;
    switchLabel_case( _case   : NToken,
                      id      : Expr,
                      _colon  : NToken) : option[Expr]
    {
      Some(id)
    }

    //switchLabel_default : Expr = "default"S ":"s;
    switchLabel_default(_default : NToken, _colon : NToken) : option[Expr]
    {
      None()
    }

    //switchSection       : SwitchCaseEntry     = (switchLabel_case / switchLabel_default) statementList?;
    switchSection(lbl : option[Expr], stmts : option[list[Statement]]) : SwitchCaseEntry
    {
      match(lbl) { 
        | Some(lbl) => SwitchCaseEntry.Case(stmts.WithDefault([]), lbl)
        | _         => SwitchCaseEntry.Default(stmts.WithDefault([]))
      }
    }

    //switchSections      : SwitchCaseEntryList = switchSection+;
    switchSections(sections : SCG.List[SwitchCaseEntry]) : list[SwitchCaseEntry]
    {
      NList.ToList(sections)
    }

    //switchStatement     : Statement           = "switch"S "("s expression ")"s "{"s switchSections? "}"s;
    switchStatement(_switch   : NToken,
                    _lparen   : NToken,
                    expr      : Expr,
                    _rparen   : NToken,
                    _lparen2  : NToken,
                    sections  : option[list[SwitchCaseEntry]],
                    _rparen2  : NToken) : Statement
    {
      Statement.Switch(expr, sections.WithDefault([]))
    }

    #endregion

    #region Iteration statements

    //whileStatement  : Statement = "while"S "("s booleanExpression ")"s embeddedStatement;
    whileStatement( _while  : NToken,
                    _lparen : NToken,
                    expr    : Expr,
                    _rparen : NToken,
                    stmt    : Statement) : Statement
    {
      Statement.While(expr, stmt)
    }

    //doStatement     : Statement = "do"S embeddedStatement "while"S "("s booleanExpression ")"s ";"s;
    doStatement(_do         : NToken,
                stmt        : Statement,
                _while      : NToken,
                _lparen     : NToken,
                cond        : Expr,
                _rparen     : NToken,
                _semicolon  : NToken) : Statement
    {
      Statement.DoWhile(cond, stmt)
    }

    //forInitializer_var  : object = localVariableDeclaration;
    forInitializer_var(decl : Statement) : object
    {
      decl
    }

    //forInitializer_expr : object = statementExpressionList;
    forInitializer_expr(init : list[Expr]) : object
    {
      init
    }

    //forCondition : Expr = booleanExpression;
    forCondition(expr : Expr) : Expr
    {
      expr
    }

    //forIterator  : ExprList = statementExpressionList;
    forIterator(iter : list[Expr]) : list[Expr]
    {
      iter
    }

    //forStatement : Statement = "for"S "("s forInitializer? ";"s forCondition? ";"s forIterator? ")"s embeddedStatement;
    forStatement( _for : NToken,
                  _lparen : NToken,
                  init : option[object],
                  _semicolon1 : NToken,
                  cond : option[Expr],
                  _semicolon2 : NToken,
                  iter : option[list[Expr]],
                  _rparen : NToken,
                  stmt : Statement) : Statement
    {
      def make_ForVar(decl) {
        Statement.ForVar(decl, cond.WithDefault(null), iter.WithDefault([]), stmt)
      }

      def make_ForExpr(init) {
        Statement.ForExpr(init, cond.WithDefault(null), iter.WithDefault([]), stmt)
      }

      match(init) {
        | Some(init) =>
          match(init) {
            | decl is Statement.VariableDecl => make_ForVar(decl)
            | init is list[Expr]             => make_ForExpr(init)
            | _ => throw ArgumentOutOfRangeException("init")
          }
        | _ => make_ForExpr([])
      }
    }

    //foreachStatement : Statement = "foreach"S "("s localVariableType identifier "in"s expression ")"s embeddedStatement;
    foreachStatement( _foreach      : NToken,
                      _lparen       : NToken,
                      variableType  : option[Expr],
                      variable      : Identifier,
                      _in           : NToken,
                      expr          : Expr,
                      _rparen       : NToken,
                      body          : Statement) : Statement
    {
      Statement.Foreach(
        variable,
        variableType.WithDefault(null),
        expr,
        body)
    }

    #endregion

    #region Jump statements

    //breakStatement        : Statement = "break"S ";"s;
    breakStatement(_break : NToken, _semicolon : NToken) : Statement
    {
      Statement.Break()
    }

    //continueStatement     : Statement = "continue"S ";"s;
    continueStatement(_continue : NToken, _semicolon : NToken) : Statement
    {
      Statement.Continue()
    }

    //gotoStatement_case    : Statement = "goto"S "case"S constantExpression ";"s;
    gotoStatement_case( _goto       : NToken,
                        _case       : NToken,
                        expr        : Expr,
                        _semicolon  : NToken): Statement
    {
      Statement.GotoCase(expr)
    }

    //gotoStatement_default : Statement = "goto"S "default"S ";"s;
    gotoStatement_default(_goto       : NToken,
                          _default    : NToken,
                          _semicolon  : NToken) : Statement
    {
      Statement.GotoDefault()
    }

    //gotoStatement_id      : Statement = "goto"S identifier ";"s;
    gotoStatement_id( _goto       : NToken,
                      label       : Identifier,
                      _semicolon  : NToken) : Statement
    {
      Statement.Goto(label)
    }

    //returnStatement       : Statement = "return"S expression? ";"s;
    returnStatement(_return     : NToken,
                    expr        : option[Expr],
                    _semicolon  : NToken) : Statement
    {
      Statement.Return(expr.WithDefault(null))
    }

    //throwStatement        : Statement = "throw"S expression? ";"s;
    throwStatement( _throw      : NToken,
                    expr        : option[Expr],
                    _semicolon  : NToken) : Statement
    {
      Statement.Throw(expr.WithDefault(null))
    }

    #endregion


    #region Try-catch-finally

    //catchClause   : TryCatchEntry     = "catch"S ("("s anyType identifier? ")"s)? block;
    catchClause(_catch    : NToken,
                exception : option[NToken * Expr * option[Identifier] * NToken],
                body      : Statement) : TryCatchEntry
    {
      def (exceptionType, variable) = match(exception) {
        | Some( (_, exceptionType, Some(variable), _) ) =>
          (exceptionType, variable)

        | Some( (_, exceptionType, None, _) ) =>
          (exceptionType, null)

        | _ => (null, null)
      }
      TryCatchEntry(exceptionType, variable, body)
    }

    //catchClauses  : TryCatchEntryList = catchClause+;
    catchClauses(clauses : SCG.List[TryCatchEntry]) : list[TryCatchEntry]
    {
      NList.ToList(clauses)
    }

    //finallyClause : Statement         = "finally"S block;
    finallyClause(_finally : NToken, body : Statement) : Statement
    {
      body
    }

    //tryStatement_catch    : TryCatchFinallyTail = catchClauses finallyClause?;
    tryStatement_catch( catchList     : list[TryCatchEntry],
                        finallyBlock  : option[Statement]) : list[TryCatchEntry] * option[Statement]
    {
      (catchList, finallyBlock)
    }

    //tryStatement_finally  : TryCatchFinallyTail = finallyClause;
    tryStatement_finally(finallyBlock  : Statement) : list[TryCatchEntry] * option[Statement]
    {
      ([], Some(finallyBlock))
    }

    //tryStatement          : Statement           = "try"S block (tryStatement_catch / tryStatement_finally);
    tryStatement( _try : NToken,
                  body : Statement,
                  tail : list[TryCatchEntry] * option[Statement]) : Statement
    {
      def (catchList, finallyBlock) = tail;
      Statement.Try(body, catchList, finallyBlock.WithDefault(null))
    }

    #endregion

    #region Checked-unchecked

    //checkedStatement    : Statement = "checked"S block;
    checkedStatement(_checked : NToken, body : Statement) : Statement
    {
      Statement.Checked(body, true)
    }

    //uncheckedStatement  : Statement = "unchecked"S block;
    uncheckedStatement(_unchecked : NToken, body : Statement) : Statement
    {
      Statement.Checked(body, false)
    }

    #endregion

    #region Lock

    //lockStatement : Statement = "lock"S "("s expression ")"s embeddedStatement;
    lockStatement(_lock   : NToken,
                  _lparen : NToken,
                  expr    : Expr,
                  _rparen : NToken,
                  body    : Statement) : Statement
    {
      Statement.Lock(expr, body)
    }

    #endregion

    #region Using

    //resourceAcquisition_var   : object = localVariableDeclaration;
    resourceAcquisition_var(decl : Statement) : object
    {
      decl
    }

    //resourceAcquisition_expr  : object = expression;
    resourceAcquisition_expr(expr : Expr) : object
    {
      expr
    }

    //usingStatement  : Statement = "using"S "("s resourceAcquisition ")"s embeddedStatement;
    usingStatement( _using    : NToken,
                    _lparen   : NToken,
                    resource  : object,
                    _rparen   : NToken,
                    body      : Statement) : Statement
    {
      match(resource) {
        | decl is Statement.VariableDecl  => Statement.UsingVar(decl, body)
        | expr is Expr                    => Statement.UsingExpr(expr, body)
        | _ => throw ArgumentOutOfRangeException("resource")
      }
    }

    #endregion

    #region Yied statements

    //yieldStatement_return : Statement = "yield"S "return"S expression ";"s;
    yieldStatement_return(_yield      : NToken,
                          _return     : NToken,
                          expr        : Expr,
                          _semicolon  : NToken) : Statement
    {
      Statement.YieldReturn(expr)
    }

    //yieldStatement_break  : Statement = "yield"S "break"S ";"s;
    yieldStatement_break( _yield      : NToken,
                          _break      : NToken,
                          _semicolon  : NToken) : Statement
    {
      Statement.YieldBreak()
    }

    #endregion

    #region Unsafe and Fixed

    //unsafeStatement : Statement = "unsafe"S block;
    unsafeStatement(_unsafe : NToken, body : Statement) : Statement
    {
      Statement.Unsafe(body)
    }

    //fixedStatement  : Statement = "fixed"S "("s localVariableDeclaration ")"s embeddedStatement;
    fixedStatement( _fixed  : NToken,
                    _lparen : NToken,
                    decl    : Statement,
                    _rparen : NToken,
                    body    : Statement) : Statement
    {
      Statement.Fixed(decl :> Statement.VariableDecl, body)
    }

    #endregion
   
    statementRecovery(pos : int, newPos : int, failedRuleId : int) : Statement
    {
      def loc = GetLocation(NToken(pos, newPos));
      def (sl, sc) = loc.StartLineColumn;
      def (el, ec) = loc.EndLineColumn;
      def (errorPos, expecteds) = GetMaxRollbackPosAndNames();
      Diagnostics.Debug.WriteLine($"($sl, $sc):($el, $ec): pos=$pos, newPos=$newPos, errorPos=$errorPos,  Expected:\n" + $<#..$(expecteds; "\r\n")#>);
      def failedRuleName = GetRuleName(failedRuleId);
      Statement.Error(loc, $<#Expected: $failedRuleName#>)
    }

    //statementList : StatementList = statement+;
    statementList(stmts : SCG.List[Statement]) : list[Statement]
    {
      NList.ToList(stmts)
    }

    //block         : Statement     = "{"s statementList? "}"s;
    block(_lparen : NToken,
          stmts   : option[list[Statement]],
          _rparen : NToken) : Statement
    {
      Statement.Block(stmts.WithDefault([]))
    }
  }
}