using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.ComputationExpressions.Async
{
  public partial abstract class Async[A]
  {
    public static FromBeginEnd(begin : AsyncCallback -> IAsyncResult, end : IAsyncResult -> A) : Async[A]
    {
      AsyncFromBeginEnd(begin, end, null);
    }

    public static FromBeginEnd(begin : AsyncCallback -> IAsyncResult, end : IAsyncResult -> void) : Async[FakeVoid]
    {
      AsyncFromBeginEnd(begin, r => { end(r); FakeVoid.Value; }, null);
    }
  }

  [Record]
  internal class AsyncFromBeginEnd[A] : Async[A]
  {
    private begin : AsyncCallback -> IAsyncResult;
    private end : IAsyncResult -> A;
    private cancel : void -> void;

    [RecordIgnore]
    private mutable _executed : bool = false;

    [RecordIgnore]
    private mutable _result : A;

    [RecordIgnore]
    private mutable _job : AsyncJobBase = null;
    
    [RecordIgnore]
    private mutable _exception : AsyncAggregateException = null;

    private Callback(asyncResult : IAsyncResult) : void
    {
      _executed = true;
      try
      {
        _result = end(asyncResult);
      }
      catch
      {
        | e => _exception = AsyncAggregateException();
               _exception.Add(e);
      }
      _job.Start();
    }

    public override Exec(job : AsyncJobBase, res : ref A) : Async[A]
    {
      if (_executed)
      {
        when(_exception != null) throw _exception;
        res = _result;
        null;
      }
      else
      {
        when (_job != null)
          throw InvalidOperationException();
        _job = job;
        _job.State = AsyncJobBase.JobState.Wait;
        _ = begin(Callback);
        this;
      }
    }

    public override Cancel() : void
    {
      when (cancel != null)
        cancel();
    }
  }
}
