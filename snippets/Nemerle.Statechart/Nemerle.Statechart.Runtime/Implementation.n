using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.ComponentModel;

namespace Nemerle.Statechart
{

  public abstract class StateMachine[TState] : INotifyPropertyChanged where TState : HsmState
  {
    [Accessor]
    protected mutable cur_state : TState;

    protected abstract Initial() : TState;

    protected mutable completion : bool;

    public IsTerminated : bool
    {
      get
      {
        cur_state == null
      }
    }

    protected abstract DispatchEvent(evt : HsmEvent) : TState;

    protected Dispatch(evt : HsmEvent) : void
    {
      TrySwitch(DispatchEvent(evt))
    }

    public override ToString() : string
    {
      if (cur_state != null) cur_state.ToString() else null
    }

    protected TrySwitch(st : TState) : void
    {
      when (st != null) Switch(st);
      OnEventComplete()
    }

    protected virtual OnSwitch() : void
    {
    }

    protected virtual OnEventComplete() : void
    {
    }

    protected Switch(st : TState) : void
    {
      cur_state = st;
      OnSwitch()
    }

    public virtual Terminate() : void
    {
      cur_state = null;
      OnTerminated()
    }

    OnTerminated() : void
    {
      Terminated?.Invoke(this, null)
    }

    public event Terminated : EventHandler;

    public event PropertyChanged : PropertyChangedEventHandler;

    protected OnPropertyChanged(name : string) : void
    {
      PropertyChanged?.Invoke(this, PropertyChangedEventArgs(name))
    }
  }

  public abstract class HsmEvent
  {
    public abstract Name : string {get;}
    public abstract FullName : string {get;}

  }

  public interface IActiveStateMachine
  {
    PostEvent(evt : HsmEvent) : void;
  }

  public abstract class HsmState
  {
  }

  public interface ITestStateMachine : INotifyPropertyChanged
  {
    Initiate()                : void;
    PostEvent(evt : string)   : void;
    PostEvent(evt : HsmEvent) : void;
    IsInState(st : string)    : bool;
    event ActionExecuted      : Action[string];
    event TransitionCompleted : EventHandler;
    Terminate()               : void;
    Events                    : IEnumerable[HsmEvent] {get;}
  }
}

