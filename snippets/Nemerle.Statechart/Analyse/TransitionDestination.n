using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using FsmState;
  
  /// <summary>
  /// Description of TransitionDestination.
  /// </summary>
  [Record]
  variant TransitionDestination
  {
    | Unknown
    | History {state : StateHistory;}
    | State {state : FsmState;}
    | NoStateChange
    | Choice 
      {
        state : FsmState;
        choice : list[StateTransition];
        else_state : option[FsmState] = None();
      }    
    | Terminate {node : PseudoStateNode.Terminate;}
    | Region {reg : ConcurrentRegion;}
    | SubRegion {reg : ConcurrentSubRegion;state : TransitionDestination;from : FsmState;}
    | Fork {reg : ConcurrentRegion;sub_tran : list[StateTransition];}
    | Join 
      {
        join : PseudoStateNode.Join;
        
        have_guard : bool
        {
          get
          {
            join.joined_tran.Exists(x => x.guard.HasValue)
          }
        }
      }
    | Final {state : FsmState.Final;region : ConcurrentRegion;completion : list[StateTransition];}
    | ForceStateChange {nested : TransitionDestination;}
    | HistoryRestore {history : StateHistory;}
    
    public ToState : FsmState
    {
      get
      {
        match (this)
        {
          | State(st)
          | History(StateHistory where (State = st))
          | Terminate(PseudoStateNode where (state = st))
          | Region(st)
          | SubRegion(reg = st)
          | Fork(reg = st)
          | Join(PseudoStateNode where (state = st))
          | Final(state = st)
          | Choice(st, _, _) => st
          | ForceStateChange(nest) => nest.ToState
          | _ => null
        }
      }
    }
  }
}
