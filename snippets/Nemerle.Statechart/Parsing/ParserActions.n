using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Peg;
using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;


namespace Nemerle.Statechart
{
    
    using StateNode;
    
  /// <summary>
  /// Description of ParserActions.
  /// </summary>
  partial class FsmParser
  {
      
    NameToState(state : NameIdentifier) : TargetType
    {                        
      def node=state.GetNode();
      match (state.TargetName)
      {
        |"H"
        |"h"
        |"history" => TargetType.History(state.pos,state.GetState())
        |"h*" 
        |"H*" => TargetType.History(state.pos,state.GetState())
        |"final" 
        |"$0" => TargetType.Final(state.pos,Final(null))
        |_ => TargetType.State(state.pos,node)
      }
    }

    create_event(tok : NToken) : TransitionNode.Event
    {
      def name=GetText(tok);
      
      def name=match (name)
                {
                  |"completion" 
                  |"_" => null
                  |_ => name
                }
      TransitionNode.Event(tok.StartPos,name)
    }

    create_action(lst : List[NToken]) : TransitionNode.Actions
    {
      def acts= lst |> TransitionNode.Action(_.StartPos,GetText(_));                
      TransitionNode.Actions(acts)          
    }

    CreateState(pos : int,name : string,attr : list[StateAttribute],decls : list[StateNode]) : StateNode.State
    {            
      def check_history(st,h)
      {
        match (h)
        {              
          |StateNode.History(state=null) => h.state=st;check_history(st,h)
          |History(default=Some(StateNode.Default(history=History(state=null) as h))) => h.state=st;check_history(st,h)
          |_ => ()
        }
      }
    
      def check_tran(state,tr)
      {
        match (tr)
        {
          |StateNode.Transition(to=TargetType.State(st) as t) when st.name=="@" || st.name=="self" => t.state=state;
          //|Transition(to=TargetType.Final(Final(st) as t)) when st==null => t.state=state;
          |Transition(to=TargetType.History(History(state=st) as t)) when st==null => check_history(state,t)
          |_ => ()
        } 
      }
          
      //Debugger.Break();   
      def decls=decls.Sort((x,y) => x.pos-y.pos);
      def state_reg=decls.Filter(x => x is State || x is Region); // находим состояния и регионы
      def regions=state_reg.FindDelimited(_ is Region).Map(_.Map(_ :> State)); // разделяем список с помощью Region
      def regions=regions.Map(Region(0,_)); // создаем регионы
      def (en : list[Entry],decls)=decls.MapFilterType();
      def (ex : list[Exit],decls)=decls.MapFilterType();
      def (states,decls)= decls.MapFilterType();
      def (trans,decls) = decls.MapFilterType();      
      def (in_trans,decls)=decls.MapFilterType();      
      def (do_act,decls)=decls.FindType();
      def (init,decls)=decls.MapFilterType();
      def (hist,_)=decls.MapFilterType();
      def history=match (hist) 
                   {
                     |[head] => ThreeOption.One(head)
                     |[f,s] => ThreeOption.Two(f,s)                     
                     |_ => ThreeOption.None() // TODO: надо отслеживать ошибку и выдавать пользователю                  
                   }
      def regions=match (regions)
                  {
                      |[] => None() // если нет регионов
                      |lst => Some(Regions(0,lst)) // если есть то создаем узел
                  }
      def en_acts= en |>< action.actions;
      def ex_acts= ex |>< action.actions;
      def en=if (!en.IsEmpty()) option.Some(StateNode.Entry(en_acts)) else option.None();
      def ex=if (!ex.IsEmpty()) option.Some(StateNode.Exit(ex_acts)) else option.None();      
      def st=State(pos,name,init,attr,en,ex,in_trans,trans,do_act,history,states,regions);
      states.Iter(x => x.Parent=st);
      trans.Iter(check_tran(st,_));
      hist.Iter(check_history(st,_));
      st
    }

    def_history(hist : StateHistory,tok : NToken,tran : option[StateNode.Default]) : StateNode.History
    {      
      //def tran= //StateNode.Default(tok.StartPos,StateNode.History(tok.StartPos,null),_);      
      match (tran)
      {
          |Some(tr) => tr.history=StateNode.History(tok.StartPos,null);
                       tr.pos=tok.StartPos;
          |_ => ()
      }
      
      StateNode.History(tok.StartPos,hist,tran);  
    }
    
  }
}
