using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using System.Timers;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of TimedEvent.
  /// </summary>
  [Record]
  class GenTimedEvent
  {
    public events : TimedEvents;
    public tran : Transition;
    
    public Source : ActiveStateConfig
    {
      get
      {
        tran.Source
      }
    }
    
    public state : HState
    {
      get
      {
        tran.OriginateFrom
      }
    }
    
    public evt  : StateEvent.Timed;
    public name : string;
    gen : StaticGenerator;
    
    public StartTranPath(tr : Transition) : TranPath
    {
      TranPath(state, PathNode.Action(state, FsmAction.Expr(StartCode(tr))) :: [], PathKind.Action)
    }
    
    public reuse_timer(tran : Transition) : bool 
    {
      if (tran.Kind == TransitionKind.History) true else
      {
        def source = tran.Source;
        def src_evts = events.event_gen.Filter(x  => x.tran.Source : object == source);
        match (src_evts.Find(x => x : object != this && x.name == name))
        {
          | Some(e) => match (e.evt.time_expr, evt.time_expr)
                        {
                          | (TimedExprType.After(a1), TimedExprType.After(a2)) => a1 != a2
                          | _                                                  => false
                        }
          | _       => false
        }
      }
    }
    
    timer_name : string
    {
      [Memoize]
      get
      {
        $"$(name)_timer"
      }
    }
    
    timer : ClassMember
    {
      [Memoize]
      get
      {
        <[decl: $(timer_name : usesite) : Timer = Timer(); ]>
      }
    }
    
    set_interval : PExpr
    {
      [Memoize]
      get
      {
        match (evt.time_expr)
        {
          | TimedExprType.After(t) => def val = t.TotalMilliseconds;
                                      <[ $(timer_name : usesite).Interval = $val; ]>
          | _                      => <[]>
        }
      }
    }
    
    init_expr : PExpr
    {
      [Memoize]
      get
      {
        match (evt.time_expr)
        {
          | TimedExprType.After    => def interval = set_interval;
                                      <[
                                         $(timer_name : usesite).AutoReset = false;
                                         $interval;
                                         $(timer_name : usesite).Elapsed += $(name : usesite);
                                      ]>
          | _                      => <[]>
        }
      }
    }
    
    public OptName : string
    {
      [Memoize]
      get
      {
        def name = name.Replace('(', '_').Replace(')', ' ').Replace(':', '_').Replace('.', '_');
        name.ToUpperFirst()
      }
    }
    
    SendEventDecl : ClassMember
    {
      get
      {
        <[decl: $(name : usesite)(_ : object, _ : ElapsedEventArgs) : void
                {
                  PostEvent(TimEvent.$(OptName : usesite).New)
                }]>
      }
    }
    
    
    public StartCode(tran : Transition) : PExpr
    {
      def reuse = if (tran != null) reuse_timer(tran) else true;
      def start = if (reuse) set_interval else <[]>;
      <[ 
        $start;
        $(timer_name : usesite).Start() 
      ]>;
    }
    
    public DispatchCode : PMatchCase
    {
      [Memoize]
      get
      {
        <[case: | TimEvent.$(OptName : usesite) => cur_state.$(name : usesite)(this) ]>
      }
    }
    
    public StopCode : PExpr
    {
      [Memoize]
      get
      {
        <[ $(timer_name : usesite).Stop() ]>
      }
    }
    
    public DeclInTopState : ClassMember
    {
      [Memoize]
      get
      {
        <[ decl: public virtual $(name : usesite)(_ : $(gen.ChartName : usesite)) : State { null } ]>
      }
    }
    
    public DeclInState(body : PExpr) : ClassMember
    {
      <[ decl: public override $(name : usesite)(fsm : $(gen.ChartName : usesite)) : State { $body } ]>
    }
    
    event_decl : ClassMember.TypeDeclaration
    {
      [Memoize]
      get
      {
        <[ variant_option: | $(OptName : usesite)
         {
           
          public override ToString() : string
          {
            Name
          }

          public override FullName : string
          {
            get
            {
              $(evt.TargetName)
            }
          }

          public override Name : string 
          {
            get
            {
              $(evt.Name)
            }
          }

          public override GetHashCode() : int
          {
            $(evt.TargetName.GetHashCode())
          }
           
         } ]>
      }
    }
    
    InstanceWoParms : ClassMember
    {
      [Memoize]
      get
      {
        <[decl:
                  public static New : HsmEvent
                  {
                    get
                    {
                      when (instance == null) 
                      {
                        instance = $(OptName : usesite)()
                      }
                      instance
                    }
                  } ]>
      }
    }

    InstanceField : ClassMember
    {
      [Memoize]
      get
      {
        <[decl: mutable static instance : HsmEvent; ]>;
      }
    }
    
    public Emit() : void
    {
      gen.Define(timer);
      gen.Define(SendEventDecl);
      gen.init_exprs ::= init_expr;
    }
    
    public EventDecl(ty : TypeBuilder) : void
    {
      def ty = ty.DefineNestedTypeWithSource(event_decl);
      ty.Define(InstanceWoParms);
      ty.Define(InstanceField);
      ty.Compile();
    }
  }
}
