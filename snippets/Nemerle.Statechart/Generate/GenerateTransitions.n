using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using StateTransition;
  using TransitionDestination;
  
  /// <summary>
  /// Description of GenerateTransitions.
  /// </summary>
  partial class StaticGenerator
  {
  
    GetTransitionFunction(tran : list[StateTransition]) : list[ClassMember]
    {
      tran |> <[ decl: $(_.Name : usesite)() : State {$(GetTransitionBody(_))} ]>;       
    }

    GetTransitionFunction(tran : list[string * StateTransition]) : list[ClassMember]
    {
      tran |> <[ decl: $(_ : usesite)() : State {$(GetTransitionBody(_[2]))} ]>;
    }
    
    GetTransitionBody(tran : StateTransition, ctx : bool = false) : PExpr
    {
      match (tran)
      {
      | StateTransition where (PathNode = acts) =>
      
          def gen_path = GenPath(acts, action_for);      
          def expr = gen_path.GetActions(true);
          def target = match (tran.Destination)
                       {                                            
                         | State(st) => if (ctx) <[ $(st.Name : usesite)() ]> else GenPath.GetExecStateName(st)
                         | NoStateChange => if (ctx) <[ fsm.cur_state ]> else <[ cur_state ]>
                         | Choice(_, tran) => DefineTransBody(tran, ctx)
                         | Terminate => if (ctx) <[ fsm.Terminate();null ]> else <[ Terminate();null ]>
                         | TransitionExecute(tran) => if (ctx) <[ fsm.$(tran.Name : usesite)(); ]> else <[ $(tran.Name : usesite)(); ]>
                         | History(StateChartHistory where (Default = Some()) as h) => def name = $"$(h)Run";
                               if (ctx) <[ fsm.$(name : usesite)();]> else <[ $(name : usesite)();]>
                         | History(StateChartHistory where (Name = n, State = p, Type = t)) =>
                             def t = if (p.BothHistories) $"$t" else "";
                          // call function of run history
                             def name = $"$(t)HistoryFrom$p";
                           if (ctx) <[ fsm.$(n : usesite).$(name : usesite)() ]> else <[ $(n : usesite).$(name : usesite)() ]>;
                         | SubRegion(reg, st) => def target = GenPath.GetExecStateName(st);
                             def region = reg.contained_in; 
                             def target_region = GenPath.GetExecStateName(region);
                             def pat = FsmState.ConcurrentRegion.get_pattern(region.sub_regions.Exclude([reg], _ == _));
                             def name = $"Initial$pat";
                                             if (ctx) 
                                             <[
                                             $(region.Name : usesite)().$(name : usesite)(fsm, $(st.Name : usesite)());
                                             $(region.Name : usesite)()
                                             ]> else                                             
                                             <[
                                             $(target_region).$(name : usesite)(this, $target);
                                             $target_region
                                             ]>
                     // entering in region
                         | Region(reg) => def target = GenPath.GetExecStateName(reg);
                                      if (ctx) 
                                      <[
                                          $(reg.Name : usesite)().Initial(this);
                                          $(reg.Name : usesite)()
                                      ]> else
                                      <[
                                          $(target).Initial(this);
                                          $target
                                      ]>
                         | Fork(reg, tran) => def target = GenPath.GetExecStateName(reg);
                             def trans = tran.Map(x => <[ $(x.Name : usesite)(); ]>);
                                     if (ctx)
                                      <[
                                          $(reg.Name : usesite)().Initial(..$trans);
                                          $(reg.Name : usesite)()
                                      ]> else                                     
                                      <[
                                          $(target).Initial(..$trans);
                                          $target
                                      ]>
                         | Join(Join(region = reg) as j) => def target = GenPath.GetExecStateName(reg);
                                       if (reg.JoinCrossed(j))
                                       if (ctx)
                                       <[
                                        $(reg.Name : usesite)().SetJoin();
                                        null
                                       ]> else
                                       <[
                                        $(target).SetJoin();
                                        null
                                       ]> else <[null]>
                         | _ => <[ null ]>
                       }
                    
                     
          def body =  <[ $expr;$target ]>;
          GetGuardedBody(tran, body)
        }
    }
    
    DefineTransBody(tr : list[StateTransition], ctx : bool = true) : PExpr
    {       
      def get_body(tr)      
      {
        def name = tr.Name;
        if (tr.MaybeInlined && GenPath.IsEmpty(tr.PathNode))
        {
             <[ $(GetTransitionBody(tr, ctx)) ]>
        } else 
        if (ctx) <[ fsm.$(name : usesite)(); ]> else <[ $(name : usesite)();]>
      }          
      
      match (tr)
      {
        | [StateTransition where (IsCrossed = true) as tr] => 
            def body =  get_body(tr);
                  <[
                  {
                  def res =  $body;
                  if (res != null) res else $(DefineTransBody(tr.ParentCrossed, ctx))
                  }
                  ]>
        | [tr]  => get_body(tr)
        | tr :: tail  =>         
            def body = get_body(tr);
                  <[
                  {
                  def res =  $body;
                  if (res != null) res else $(DefineTransBody(tail, ctx))
                  }
                  ]>                  
        |  _  => <[ null ]>
      } 
          
    }
    
    DefineTransitions(ty : TypeBuilder, st : FsmState) : void
    {               
      def defs = if (st.IsTopState)
                 {
                   analyser.TransitionEvents |> <[decl: public virtual $(_.Name : usesite)(_ : $(ChartName : usesite)) : State {null}]>;                  
                 }
                 else
                 {
                   if (st.Lived)
                   {
                     st.work_transition_group.MapToList((x, y)  => <[decl: 
                       public override $(x : usesite)(fsm : $(ChartName : usesite)) : State {$(DefineTransBody(y |> (_ : StateTransition)))}]>)
                   } else [];
                 }
      Define(ty, defs)      
    }

    DefineHistoryPaths(builder : TypeBuilder, st : FsmState) : void
    {        
      def create_body(hist)
      {        
        <[ fsm.$(hist.Name : usesite)(); ]>
      }
                
      def get_name(h, x)
      {
        | (StateChartHistory where (State = FsmState where (BothHistories = true), Type = t), x) => $"$(t)HistoryFrom$x"
        | (_, x) => $"HistoryFrom$x"        
      }
        
      def get_run_hist_body(hist)
      {
        | StateTransition.Default(From = from, history = StateChartHistory where (SimpleName = name) as hist) as t =>
            def hname = get_name(hist, from.Name);
                <[ 
                if ($(name : usesite) != null) // определяем выбор в случае если есть переход по умолчанию в случ.если нет истории
                    $(name : usesite).$(hname : usesite)(this); else $(DefineTransBody([t], false));
                ]> 
      }
      
      if (st.IsTopState) // if it is top state
      {
        def names = analyser.HistoryStates |> get_name(_, _.State.Name); // get name of history states
        def decls = names |> <[decl: public virtual $(_ : usesite)(_ : $(ChartName : usesite)) : State {null} ]>; // create transition methods for history states
        Define(builder, decls);
      } else
      {
        when (st.HaveHistoryPaths && st.Lived) // if state has history paths or it is lived
        {
          def paths  =  st.HistoryPaths;
          def name  =  paths |> (get_name(_[2].History, _), _[2]); // create transition names
          def decl  =  name.MapToList((n, y) => <[decl: public override $(n : usesite)(fsm : $(ChartName : usesite)) : State {$(create_body(y))}]>);
          Define(builder, decl);
        }        
      }
            
      def def_history(h)
      {                
        def hist_func =  h.Head.SimpleName;
        def hist_field =  <[decl: mutable $(hist_func : usesite) : State;]>;
        def body = <[ $(hist_func : usesite)  =  cur_state;]>;
            // save history function
        def save =  <[decl: $($"$(hist_func)Save" : usesite)() : void 
            {
                $body
            }]>;
        def defs = [save, hist_field];        
        // default transitions
        def defaults = h |>< (_.Default.Map(x  => [<[decl: $($"$(x.history)Run" : usesite)() : State {$(get_run_hist_body(x));} ]>]).WithDefault([]));
        def defs = defaults + defs;            
        Define(defs); // add definition to state class
      }
      
      when (st.history.HasValue)
      {
        def hist = st.UsedHistory;        
        when (!hist.IsEmpty()) def_history(hist);
      }
    }
    
    DefineTransitionActions() : void
    {              
      def trans  =  analyser.all_transitions;
      
      def trans = trans.Filter(x => !(x.MaybeInlined && GenPath.IsEmpty(x.PathNode)));      
      def body = GetTransitionFunction(trans);
            
      // history paths
      def body_name = analyser.HistoryPaths |> (Name, _.transition);
      def hist = GetTransitionFunction(body_name);
      Define(hist + body);
    }
    
  }
}
