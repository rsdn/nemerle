using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Collections;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Идентификатор состояния
  /// </summary>
  class StateId
  {
    public mutable Id : ulong;
    public mutable LongId : BitArray;
    state : FsmState;
    mutable analyser : FsmAnalyser;
    
    public this(st : FsmState)
    {
      state = st;
    }
    
    [Accessor]
    mutable length : int;
    
    [Accessor]
    mutable mask_type : TypeCode;
    
    public Result : list[int]
    {
      get
      {
        def res = array(Length);
        LongId.CopyTo(res, 0);
        res.NToList()
      }
    }
    
    public GetId() : PExpr * PExpr
    {
      def get_long_id() : PExpr
      {        
        def res = Result;
        def pars =  res |> <[ $(_ : int)]>;
        <[ LongId(..$pars) ]>
      }
      
      match (MaskType)
      {
        | TypeCode.UInt32  => (<[uint]>, <[ $((Id :> uint) : uint) ]>)
        | TypeCode.UInt64  => (<[ulong]>, <[ $(Id : ulong) ]>)
        | TypeCode.Empty 
        | _  => (<[LongId]>, <[ $(get_long_id()) ]>)
      }
    }
     
    public CreateNumId(i : int) : void
    {
      Id = i :> ulong;
    }
    
    public CreateMask(analyser : FsmAnalyser) : void
    {      
      this.analyser = analyser;
      def count = analyser.Statemap.Count;
      length = MathUtils.UpperDiv(count, 32);
      mask_type = match (Length)
                 {
                   | x when x <= 1 => TypeCode.UInt32
                   | x when x <= 2 => TypeCode.UInt64
                   | _ => LongId = BitArray(count);
                         TypeCode.Empty;
                 }
        
      def create_mask()
      {
        when (Id  != 0ul)
          Id =  (1 : ulong << (Id :> int - 1)) | state.SuperState.Id.Id;
      }

      def create_long_mask()
      {
        when (Id != 0ul)
        {          
          LongId.Set(Id :> int - 1, true);
          LongId = LongId.Or(state.SuperState.Id.LongId);
        }
      }
      
      if (MaskType != TypeCode.Empty)
      {
        create_mask()
      } else create_long_mask();
    
      state.SubStates.Iter(x => x.Id.CreateMask(analyser));
    }
  }
}
