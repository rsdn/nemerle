using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Diagnostics;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{

  using StateTransition;

  [Record]
  [DebuggerDisplay("State  =  {Name}")]
  public partial variant FsmState
  {
    | State  
        {
          public this(st : StateNode.State, reference : bool = false)
          {
            base();
            state = st;
            this.reference = reference || state.reference;
            when (!Reference)
            {
              Name = st.name;
              do_activity = st.do_activity |> StateAction.Do(_.action.ActionList, this, _.flags);
              internal_tran = Internal.Create(this, st.internal_tran);
              entry = st.entry |> StateAction.Entry(_.action.ActionList, this);
              exit = st.exit |> StateAction.Exit(_.action.ActionList, this);
              history =  StateHistory.Create(this, st.history);
              transitions = Transition.Create(this, st.transitions);
              initial = st.initial |> Initial(this, _);
              def (regs, subst) = st.sub_states.Partition(x => x.regions.HasValue);
              def regs = regs.Map(x => ConcurrentRegion(this, x.regions.Value));
              super_state = if (st.SuperState != null) State(st.SuperState, true); else null;
              sub_states = (subst |> State(_)) + regs;
              pseudo_states = st.pseudo_states |> PseudoStateNode.Create(_.node)
            }
          }
      
        }
    | Final
        {

          [Accessor(flags = WantSetter)]
          public mutable completion : list[StateTransition] = [];
          
          public this(super : FsmState, reference : bool =  false)
          {
            base();
            this.super_state = super;
            name =  $"$(super_state)Final";
            this.reference = reference;
          }

          public override UpdateReference(map : StateMap) : void
          {
            Analyser = map.Analyser;
            when (super_state.Reference) super_state = map[super_state.name];
          }
        }

    | ConcurrentSubRegion
        {
          index : int;
          contained_in : ConcurrentRegion;
      
          public this(regions : ConcurrentRegion, st : StateNode.SubRegion, idx : int)
          {
            contained_in = regions;
            do_activity = st.do_activity |> StateAction.Do(_.action.ActionList, this, _.flags);
            internal_tran = Internal.Create(this, st.internal_tran);
            entry = st.entry |> StateAction.Entry(_.action.ActionList, this);
            exit = st.exit |> StateAction.Exit(_.action.ActionList, this);
            history = StateHistory.Create(this, st.history);
            transitions = Transition.Create(this, st.transitions);
            initial = st.initial |> Initial(this, _);
            pseudo_states = st.pseudo_states |> PseudoStateNode.Create(_.node);
            sub_states = st.sub_states |> State(_);
            sub_states.Iter(x => x.super_state = this);
            index = idx;
            super_state = regions;
          }
    
          public override UpdateReference(map : StateMap) : void
          { 
            base.UpdateReference(map);
          }
      
          public transitions_to_final : list[StateTransition]
          {
            get
            {
              (LivedSubStatesNested |>< transitions).Filter(x => x.ActualTo is Final)
            }
          }
          
          public FinalState : option[FsmState]
          {
            get
            {
              sub_states.Find(_ is Final)
            }
          }
        }
    
    | ConcurrentRegion
        {
          sub_regions : list[ConcurrentSubRegion] = [];
    
          mutable exit_pattern : Map[string, list[ConcurrentSubRegion]] = Map();
          mutable enter_pattern : Map[string, list[ConcurrentSubRegion]] = Map();
    
          public this(st : FsmState, node : StateNode.Region)
          {
            name = node.state.name;
            super_state = st;
            transitions = Transition.Create(this, node.transitions);
            sub_regions = node.regions.MapIndex((x, i) => ConcurrentSubRegion(this, x, i));
            sub_states = sub_regions |> _ : FsmState;
          }

          public static get_pattern(reg : list[ConcurrentSubRegion]) : string
          {
            def idx = (reg |> index).Sort(_ - _);
            def idx = idx |> _.ToString();
            string.Join("", idx)
          }
          
          public DefineExitPattern(reg : list[ConcurrentSubRegion]) : void
          {
            def str = get_pattern(reg);
            when (!exit_pattern.Contains(str)) exit_pattern = exit_pattern.Add(str, reg);
          }
          
          public DefineEnterPattern(reg : list[ConcurrentSubRegion]) : void
          {
            def str = get_pattern(reg);
            when (!enter_pattern.Contains(str)) enter_pattern = enter_pattern.Add(str, reg);
          }

          public RegionExcept(st : list[ConcurrentSubRegion]) : ConcurrentSubRegion
          {
            match (sub_regions.Exclude(st, _ == _))
            {
              | [sub] => sub
              | head :: _ => head
              | _ => null
            }
          }

          public RegionsExcept(st : list[FsmState]) : list[ConcurrentSubRegion]
          {
            sub_regions.Filter(x => !x.AllSubStates.Exists(y => st.Exists(z => y == z)) && !st.Exists(y => y == x))
          }
          
          public RegionsExcept(st : FsmState) : list[ConcurrentSubRegion]
          {
            sub_regions.Filter(x => !x.AllSubStates.Contains(st) && x != st)
          }

          public Joins : list[PseudoStateNode.Join]
          {
            get
            {
              def get_join(t, lst)
              {
                match (t)
                {
                  | StateTransition where (To = TransitionTarget.PseudoState(Join() as j)) => j :: lst
                  | _ => lst
                }
              }
      
              def tran = AllLivedSubStatesNested.Map(x => x.all_transitions.FoldLeft([], get_join)).Flatten();
              tran.RemoveDuplicatesSort((x, y) => FsmAnalyser.StringComparator(x.name, y.name));
            }
          }
          
          public HaveJoinTransitions : bool
          {
            get
            {
              !Joins.IsEmpty()
            }
          }
          
          /// <summary>
	        /// Indicates that maybe external transitions from some points of orthogonal state out of region not only completion
	        /// </summary>
          public MayTakePlaceForcedTransitions : bool {get;set;}
          
          public HaveFinalCompletion : bool
          {
            get
            {
              def st = this :: SuperStates;
              HaveFinal && st.Exists(x => !x.completion_transitions.IsEmpty())
            }
          }
          
          public NeedHistorySave : bool {get;set;}
          public FinalStates : list[FsmState]
          {
            get
            {
              if (HaveFinal) sub_regions |> FinalState.Value else []
            }
          }
          
          /// <summary>
		      /// Indicates that maybe transitions from outer states forking control to this orthogonal region
		      /// </summary>
          public MayForkingFromOut : bool {get;set;}
        }
        
    public mutable super_state_cross_transitions : list[StateTransition] = [];
    
    public this()
    {
      Id = StateId(this)
    }

    public mutable do_activity : option[StateAction.Do];
      
    [Accessor] mutable sub_states : list[FsmState] = [];
    public mutable history : TwoOption[StateHistory] = TwoOption.None();
    public mutable pseudo_states : list[PseudoStateNode] = [];

    [Accessor] mutable history_paths : list[string * HistoryPath] = []; // paths from outer history
    
    mutable state : StateNode.State;
    
    [Accessor] mutable initial : list[StateTransition] = [];
    public mutable entry : option[StateAction.Entry] = None();
    public mutable exit  : option[StateAction.Exit] = None();
    [Accessor] mutable internal_tran : list[StateTransition] = [];
    [Accessor] mutable transitions : list[StateTransition] = [];
    [Accessor] mutable super_state  : FsmState;
    
    public paths : TransitionPaths {get;set;}
    /// <summary>
    /// Indicates what fsm lived in this state and it not only composite for others
    /// </summary>
    public Lived : bool {get;set;}
    /// <summary>
    /// Hierarchy position
    /// </summary>
    public hierarchy_pos : int {get;set;}
    
    public IsTopState : bool
    {
      get
      {
        Name == TopStateName
      }
    }
    
    public static TopStateName  =  "[Top]";

    [Accessor]
    mutable reference : bool;
    private mutable name : string;

    public Analyser : FsmAnalyser {get;set;}

    public Id : StateId;

    public Name : string
    {
      get
      {
        def get_name() {if (name != null) name else AstName;}
        match (this)
        {
          | Final => $"$(super_state)Final"
          | State => get_name()
          | ConcurrentRegion => get_name()
          | ConcurrentSubRegion(index = idx, contained_in = r) => $"$(r)SubRegion$idx";
        }
      }
      set
      {
        name = value;
      }
    }

    public join_completion_transitions : list[StateTransition]
    {
      get
      {
        completion_transitions.Filter(x => x.To is TransitionTarget.PseudoState(Join()))
      }
    }
    
    public AstName : string
    {
      get
      {
        state.name
      }
    }

    /// <summary>
	  /// Events for which reacts state
	  /// </summary>
    public events : list[StateEvent]
    {
      get
      {
        def evts = work_transitions |> Trigger;
        evts.RemoveDuplicatesSort(_.CompareTo(_))
      }
    }

    /// <summary>
	  /// Events for which reacts state or nested states
	  /// </summary>    
    public events_nested : list[StateEvent]
    {
      get
      {
        def evts = events + (AllLivedSubStates |>< events);
        evts.RemoveDuplicatesSort(_.CompareTo(_));
      }
    }
        
    public SuperStatesInclude : list[FsmState]
    {
      get
      {
        this :: SuperStates
      }
    }
    
    public PartOfSubRegion : option[ConcurrentSubRegion]
    {
      get
      {
        SuperStatesInclude.FindType()[0]
      }
    }

    public PartOfSubRegions : list[ConcurrentSubRegion]
    {
      get
      {
        SuperStatesInclude.FilterType()
      }
    }

    public PartOfTopRegion : option[ConcurrentRegion]
    {
      get
      {
        match (PartOfRegions)
        {
          | [] => None()
          | lst => Some(lst.Last())
        }
      }
    }
   
    public PartOfRegion : option[ConcurrentRegion]
    {
      get
      {
        SuperStatesInclude.FindType()[0]
      }
    }
    
    public PartOfRegions : list[ConcurrentRegion]
    {
      get
      {
        SuperStatesInclude.FilterType()
      }
    }

    public PartOfRegionsOf(st : FsmState) : list[ConcurrentRegion]
    {
      SuperStatesInclude.FirstElems(x => x == st).FilterType();
    }
    
    public override ToString() : string {Name}

    public virtual UpdateReference(map : StateMap) : void
    {
      Analyser = map.Analyser;
      when (super_state != null)
        when (super_state.Reference)
        {
          when (super_state.AstName == null) super_state.Name = TopStateName;
          super_state = map[super_state.Name];
        }
      initial.Iter(_.UpdateReference(map));
      transitions.Iter(_.UpdateReference(map));
      history.Iter(_.UpdateReference(map));
      pseudo_states.Iter(_.UpdateReference(map));
    }

    
     
    public DefineInitial(st : FsmState) : void
    {
      match (this)
      {
        | ConcurrentSubRegion(contained_in = r) => initial ::= Initial(r, st)
        | _ => initial ::= Initial(this, st)
      }
    }

    public AddSubState(st : FsmState) : void
    {
      st.Analyser = Analyser;
      sub_states = sub_states + [st];
    }
    
    public DefineTransition(tr : StateTransition) : void
    {
      | Transition as tr => transitions ::= Transition(this, tr)
      | Internal as tr => internal_tran ::= Internal(this, tr)
      | Initial as tr => initial ::= tr
      | _ => ()
    }

    public DefineTransitions(tr : list[StateTransition]) : void
    {
      tr.Iter(DefineTransition);
    }

    public DefineHistoryPath(st : string, nodes : HistoryPath) : void
    {
      history_paths ::= (st, nodes);
    }

    public DefineHistoryPathRecurse(st : string, nodes : HistoryPath) : void
    {
      def reg = this is ConcurrentRegion;
      when (Lived || reg) DefineHistoryPath(st, nodes);
      when (!reg) sub_states.Iter(x  => x.DefineHistoryPathRecurse(st, nodes))
    }

    public DeleteTransitions(tr : list[StateTransition]) : void
    {
      def (tran, other) = tr.PartitionType();
      def (int_tran, other) = other.PartitionType();
      def (init, _) = other.PartitionType();
      initial = initial.Exclude(init, (x, y) => StateTransition.FullComparer(x, y) == 0);
      internal_tran = internal_tran.Exclude(int_tran, (x, y) => StateTransition.FullComparer(x, y) == 0);
      transitions = transitions.Exclude(tran, (x, y) => StateTransition.FullComparer(x, y) == 0)
    }

    public DeleteTransitionsNested(tr : list[StateTransition]) : void
    {
      DeleteTransitions(tr);
      AllSubStates.Iter(x => x.DeleteTransitionsNested(tr));
    }
    
    public TraverseTransitions() : void
    {
      paths = TransitionPaths(this);
      paths.TraverseTransitions(true);
    }
    
    public StateIter(iter : FsmState -> void) : void
    {
      iter(this);
      sub_states.Iter(_.StateIter(iter));
    }
  }
}
