using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  
  using StateTransition;
  using ListUtils;
  /// <summary>
  /// Description of FsmStateActions.
  /// </summary>
  public partial variant FsmState
  {
    
    public transitions_nested : list[StateTransition]
    {
      get
      {
        transitions + (AllLivedSubStatesNested |>< transitions)
      }
    }
    
    public completion_transitions : list[StateTransition]
    {
      get
      {
        change_transitions.Filter(_.IsCompletion)
      }
    }

    public change_transitions : list[StateTransition]
    {
      get
      {
        super_state_cross_transitions + transitions
      }      
    }
    
    public all_transitions : list[StateTransition]
    {
      get
      {
        change_transitions + internal_tran
      }
    }
        
    public static create_transition_group(tr : list[StateTransition]) : Map[string, list[StateTransition]]
    {
      def group = tr.Group((x, y)  => FsmAnalyser.StringComparator(x.FriendlyTriggerName, y.FriendlyTriggerName));
      def lst = group |> (_.Head.FriendlyTriggerName, _);
      Map(lst)
    }

    public HaveFinal : bool
    {
      get
      {
        match (this)
        {
          | ConcurrentRegion(sub_regions = r) => r.All(x => x.HaveFinal)
          | _ => sub_states.Exists(_ is Final)
        }
      }
    }

    public grouped_transitions : Map[string, list[StateTransition]]
    {
      get
      {
        create_transition_group(all_transitions)
      }
    }

    public this_cross_transitions : Map[string, list[StateTransition]]
    {
      get
      {
        create_transition_group(this_transitions).Filter((_, x)  => x.Length > 1)
      }
    }

    public work_transition_group : Map[string, list[StateTransition]]
    {
      get
      {
        create_transition_group(work_transitions)
      }
    }

    public cross_transitions : Map[string, list[StateTransition]]
    {
      get
      {
        grouped_transitions.Filter((_, x)  => x.Length > 1)
      }
    }

    /// <summary>
	  /// Transitions defined direct from this state
	  /// </summary>
    public this_transitions : list[StateTransition]
    {
      get
      {
        all_transitions.Filter(x  => x.From == this)
      }
    }

    public GetHistory(type : StateHistory) : StateHistory
    {
      match (history)
      {
        | One(h)  => h
        | Two(f, s)  => if (f.Type == type.Type) f else s
        | _  => null
      }
    }

    /// <summary>
	  /// State comparing
	  /// </summary>
    public CompareTo(b : FsmState) : int
    {
      FsmAnalyser.StringComparator(Name, b.Name)
    }

    public static @==(a : FsmState, b : FsmState) : bool
    {
      | (null, null)  => true
      | (a, null)  => a : object  ==  null
      | (null, b)  => b : object  ==  null
      | (a, b)  => FsmAnalyser.StrEq(a.Name, b.Name)
    }

    public static @!=(a : FsmState, b : FsmState) : bool
    {
        !(@==(a, b))
    }

    public HaveHistoryPaths : bool
    {
      get
      {
        history_paths != null && history_paths.Count() != 0
      }
    }

    public all_transitions_with_init : list[StateTransition]
    {
      get
      {
        all_transitions + initial
      }
    }
    /// <summary>
	  /// Normal and Internal Transitions
	  /// </summary>
    public combined_transitions : list[StateTransition]
    {
      get
      {        
        transitions + internal_tran
      }
    }

    public incoming_transitions : list[StateTransition]
    {
      get
      {
        Analyser.States |>< _.all_transitions.Filter(x => x.AtLeastTo == this)
      }
    }
    
    /// <summary>
	  /// Work transitions
	  /// </summary>
    public work_transitions : list[StateTransition]
    {
      get
      {
        if (Lived)
        {
          //def trans = transitions.Filter(x => !x.IsCompletion);
          def trans = transitions + internal_tran;
          def trans = trans.Filter(x => !x.CrossFor);
          trans.Exclude(_.NotWork)
        } else []
      }
    }

    /// <summary>
	  /// All sub states at all nesting levels
	  /// </summary>
    public AllSubStates : list[FsmState]
    {
      get
      {
        sub_states + (sub_states |>< AllSubStates)
      }
    }

    /// <summary>
	  /// All lived sub states
	  /// </summary>
    public AllLivedSubStates : list[FsmState]
    {
      get
      {
        LivedSubStates + (sub_states |>< AllLivedSubStates)
      }
    }

    /// <summary>
	  /// All lived sub states
	  /// </summary>
    public AllLivedSubStatesNotRegions : list[FsmState]
    {
      get
      {
        def lived = LivedSubStates;
        def (reg, not_reg) = sub_states.Partition(_ is ConcurrentRegion);
        def reg = reg.Filter(x => x.LivedNested);
        def not_reg = not_reg |>< AllLivedSubStatesNotRegions;
        lived + reg + not_reg
      }
    }
    
    /// <summary>
	  /// Direct lived sub states without consider of living nesting states of this substates
	  /// </summary>
    public LivedSubStates : list[FsmState]
    {
      get
      {
        sub_states.Filter(_.Lived)
      }
    }
    
    /// <summary>
	  /// All Lived substates with consider of nesting state life on all nesting levels
	  /// </summary>
    public AllLivedSubStatesNested : list[FsmState]
    {
      get
      {
        sub_states.Filter(x => x.LivedNested) + (sub_states |>< AllLivedSubStatesNested)
      }
    }
    
    /// <summary>
	  /// Is it Lived state, consider on nesting lived states
	  /// </summary>
    public LivedNested : bool
    {
      get
      {
        Lived || !AllLivedSubStates.IsEmpty()
      }
    }
    
    /// <summary>
	  /// Direct lived substates with consider of nesting state life
	  /// </summary>
    public LivedSubStatesNested : list[FsmState]
    {
      get
      {
        sub_states.Filter(_.LivedNested)
      }
    }
    
    /// <summary>
	  /// Sub states included in the history of this state
	  /// </summary>
    public SubStatesParticipateInHistory : list[FsmState]
    {
      get
      {
        history.MapToList(SubStatesParticipateIn).Flatten()
      }
    }

    public SubStatesParticipateIn(hist : StateHistory) : list[FsmState]
    {
      | StateHistory.Deep  => AllLivedSubStatesNotRegions
      | StateHistory.Shallow  => LivedSubStatesNested
      | _  => []
    }

    /// <summary>
	  /// History pseudo-states in which participate this state
	  /// </summary>
    public ParticipateInHistory : list[StateHistory]
    {
      get
      {
        Analyser.HistoryStates.Filter(x  => x.SubStatesParticipateInHistory.Contains(this))
      }
    }

    public SuperStates : list[FsmState]
    {
      get
      {
        def loop(p, res)
        {
          match (p)
          {
            | null  => res.Rev()
            | p  => loop(p.SuperState, p :: res)
          }
        }

        loop(SuperState, [])
      }
    }
    
    /// <summary>
	  /// Use both types of history in state
	  /// </summary>
    public BothHistories : bool
    {
      get
      {
        history.IsTwo
      }
    }

    public UsedHistory : list[StateHistory]
    {
      get
      {
        history.Value.Filter(_.Used)
      }
    }
    
    public static CreateState(st : StateNode.State) : FsmState
    {
      if (st != null) State(st, true) else null
    }
    
    public region_exit_path : PathNode {get;set;}
  }
}
