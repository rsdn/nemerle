using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Peg;
using Nemerle;
using System.Diagnostics;
using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{
  
  using StateNode;  
  using TransitionNode;
  using PseudoStateAstNode;
  
  public partial class FsmParser
  {
    name(tok : NToken) : NameIdentifier.Name
    {
      NameIdentifier.Name(tok.StartPos, GetText(tok))
    }
    
    fully_qualified(id1 : NToken, id2 : NToken) : NameIdentifier.QualifiedName
    {
      NameIdentifier.QualifiedName(id1.StartPos, GetText(id1), GetText(id2))
    }

    guard(tok : NToken) : Guard
    {
      Guard(tok.StartPos, GetText(tok))
    }

    simple_guard(tok : NToken) : Guard
    {
      Guard(tok.StartPos, GetText(tok))
    }
    
    simple_target(name : NameIdentifier) : TargetType
    {
      def node = name.GetNode();
      def tstate = name.GetState();
      match (name.TargetName)
      {
        | "H"
        | "h" => TargetType.History(name.pos, StateNode.History(0, HistoryType.Shallow, None(), tstate, true))
        | "history"  => TargetType.History(name.pos, name.GetState())
        | "h*" 
        | "H*" => TargetType.History(name.pos, StateNode.History(0, HistoryType.Deep, None(), tstate, true))
        | "final" 
        | "$0"  => TargetType.Final(name.pos, Final(null))
        | "terminate"
        | "x"
        | "X" => TargetType.PseudoState(name.pos, PseudoStateAstNode.Terminate(tstate, 0))
        | x when (x.IndexOf('(') != -1 ) => TargetType.Named(name.pos, tstate, name.TargetName.Trim('(',')'))
        | _ => TargetType.State(name.pos, node)
      }

    }
    
    transition_mark(guard : option[Guard], act : option[Actions], target : TargetType) : StateNode.Transition
    {
      Transition(guard, act, target)
    }
      
    default_transition(act : option[Actions], target : TargetType) : Default
    {
      match (target)
      {
        | State(st) => Default(act, st)
        | _ => Default(act, null)
      }
    }
    
    events(id : NToken, evt : List[NToken]) : List[Event]
    {
      def lst = id :: evt.NToList();
      def lst = lst |> create_event(_);
      lst.ToList()
    }
    
    simple_transition(evt : List[Event], tran : StateNode.Transition) : StateNode
    {
      Transition(evt, tran)
    }
    
    simple_attrs(attr : List[List[NToken]]) : List[NToken]
    {
      attr.Flatten().ToList()
    }
    
    attributes(attr : List[NToken]) : List[StateAttribute]
    {
      def get_attr(at)
      {
        match (GetText(at))
        {
          | "Initial" => StateAttribute.Initial(at.StartPos)
          | str => StateAttribute.Unknown(at.StartPos, str)
        }
      }
      
      attr.Map(get_attr).ToList()
    }

    trans_act_list(lst : List[NToken]) : Actions
    {
      create_action(lst)
    }
    
    block_action_list(lst : List[NToken]) : Actions
    {
      create_action(lst)
    }
    
    empty_action_list(tok : NToken) : Actions
    {
      Actions(tok.StartPos, [])
    }
    
    delim_action_list(lst : List[NToken]) : Actions
    {      
      create_action(lst)
    }
          
    entry(key : NToken, acts : option[Actions]) : Entry
    {      
      acts.MapDefault(Entry(key.StartPos, _), Entry(key.StartPos));
    }

    exit(key : NToken,  acts : option[Actions]) : Exit
    {
      acts.MapDefault(Exit(key.StartPos, _), Exit(key.StartPos));
    }

    do_activity(attr : option[List[NToken]], key : NToken, acts : Actions) : DoActivity
    {
      def get_attr(at, res)
      {
        match (GetText(at))
        {
          | "Concurrently" => (res | ActivityFlag.Concurrently)
          | "NoWait" => (res | ActivityFlag.NoWaitCancel)
          | "NotLong" => (res | ActivityFlag.NotLong)
          | _ => ActivityFlag.None
        }
      }
      
      def flags = match (attr)
                  {
                    | Some(at) => at.FoldLeft(ActivityFlag.None, get_attr);
                    | _ => ActivityFlag.None
                  }
      DoActivity(key.StartPos, acts, flags)
    }
    
    internal_trans(evt : NToken, guard : option[Guard], acts : Actions) : InternalTransition
    {
      InternalTransition(evt.StartPos, [create_event(evt)], guard, acts)
    }
    
    initial_trans(name : NToken, tran : StateNode.Transition) : Initial
    {
      Initial(name.StartPos, tran)
    }

    state(attr : option[List[StateAttribute]], name : NToken, nodes : List[StateNode]) : State
    {
      def name_text = GetText(name);
      def attr = attr.MapDefault(_.NToList(), []);
      CreateState(name.StartPos, name_text, attr, nodes.NToList());
    }
      
    flags(tok : List[NToken]) : MachineFlags
    {
      def proc_tok(tok, flag)
      {
        def nflag = match (GetText(tok))
                    {
                      | "debug"  => MachineFlag.Debug
                      | "auto_initial"  => MachineFlag.AutoInitial
                      | "local_default"  => MachineFlag.LocalDefault
                      | "transition_completed_events"  => MachineFlag.TransitionCompleted
                      | _  => MachineFlag.None
                    }
        nflag |  flag
      }
      
      MachineFlags(tok.FoldLeft(MachineFlag.None, proc_tok));
    }
    
    history_deep(tok : NToken, tran : option[StateNode.Default]) : History
    {
      def_history(HistoryType.Deep, tok, tran)
    }

    history_shallow(tok : NToken, tran : option[StateNode.Default]) : History
    {
      def_history(HistoryType.Shallow, tok, tran)
    }
    
    history_long(type : NToken, tran : option[StateNode.Default]) : History
    {
      match (GetText(type))
      {
        | "Deep"
        | "deep"  => def_history(HistoryType.Deep, type, tran)
        | "Shallow"
        | "shallow"  => def_history(HistoryType.Shallow, type, tran)
        | _  => def_history(HistoryType.None, type, tran)
      }
    }
    
    terminate_node(tok : NToken) : PseudoState
    {
      StateNode.PseudoState(tok.StartPos, PseudoStateAstNode.Terminate(null, tok.StartPos))
    }
    
    join(tok : NToken, name : NToken, target : TargetType) : PseudoState
    {
      StateNode.PseudoState(tok.StartPos, PseudoStateAstNode.Join(null, tok.StartPos, GetText(name), target))
    }
    
    junction_target(node : Junction) : TargetType
    {
      TargetType.PseudoState(node.pos, node)
    }
    
    junction(tok : NToken, name : NToken, node : Junction) : PseudoState
    {
      def pos = tok.StartPos;
      node.name = GetText(name);
      node.pos = pos;
      PseudoState(pos, node)
    }

    create_junc_node(g : option[Guard], a: option[Actions], t : TargetType) : JunctionNode
    {
      JunctionNode(0, g, a, t)
    }
    
    junction_body(body : List[option[Guard] * option[Actions] * TargetType]) : Junction
    {
      def nodes = body |> create_junc_node(_);
      Junction(null, 0, null, nodes)
    }
    
    choice_target(tok : NToken, body : Junction) : TargetType
    {
      def pos = tok.StartPos;
      TargetType.PseudoState(pos, Choice(null, pos, null, body.nodes))
    }
    
    choice(tok : NToken, name : NToken, _ : NToken, body : Junction) : StateNode.PseudoState
    {
      def pos = tok.StartPos;
      PseudoState(pos, Choice(null, pos, GetText(name), body.nodes))
    }
    
    merge(tok : NToken, name : NToken, guard : option[Guard], act : option[Actions], target : TargetType) : StateNode.PseudoState
    {
      def pos = tok.StartPos;
      PseudoState(pos, Merge(null, pos, GetText(name), JunctionNode(pos, guard, act, target)))
    }

    trans_target(tar : TargetType, targets : List[TargetType]) : TargetType
    {
      if (targets.Count() == 0) tar else 
      {
        def targets = tar :: targets.NToList();
        TargetType.PseudoState(0, PseudoStateAstNode.Fork(null, 0, targets))
      }
    }
    
    stateMachine(sp : NToken, flags : option[MachineFlags],  st : List[StateNode]) : StateMachine
    {
      def st = CreateState(sp.StartPos, null, [], st.NToList());
      def flags = flags.WithDefault(MachineFlags(MachineFlag.None));
      StateMachine(flags, st);
    }
    
    region(beg : NToken, _ : NToken) : StateNode.SubRegion
    {
      StateNode.SubRegion(beg.StartPos)
    }
  }

}
