using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Console;
using Nemerle.Statechart;
using System.Threading;

  [statechart(<#

  flags testing;
  //flags debug;
  
  name "level conflict with guards and two no guard transitions";
  a => C; // W: Transition "TransitionTopToCBya" is overriden by \[TransitionBToCBya, TransitionDToEBya, TransitionCToBBya, TransitionEToBBya\], due to execute priority rules, because this transitions is always enabled
  
  state D
  {
    
    a => E;
  
    state A
    {
      a [a2] => C;
    
      state B
      {
        a [a1] => C;
      }

      state C
      {
        a => B;
      }
      
      state E
      {
        a => B;
      }
    }
  
  }
  #>)]
  public class example
  {
    public a1 : bool {get;set;}
    public a2 : bool {get;set;}
  }

  module Test
  {
    Main() : void
    {
      def fsm = example();
      def tester = fsm.GetTester();
      tester.ConsoleTestInit();
      tester.Initiate("B", "EntryD", "EntryA", "EntryB");
      fsm.a1 = true;
      tester.Test("a", ["B"], ["C"]);
      tester.Test("a", ["C"], ["B"]);
      fsm.a1 = false;
      fsm.a2 = true;
      tester.Test("a", ["B", "A"], ["A", "C"]);
      tester.Test("a", ["C"], ["B"]);
      fsm.a2 = false;
      tester.Test("a", ["B", "A", "D"], ["D", "A", "E"]);
      tester.Test("a", ["E"], ["B"]);
      fsm.Terminate();
    }
  }
  
/*
BEGIN-OUTPUT
EntryD
EntryA
EntryB
[B]
Ok
Processing a 'a' 
ExitB
EntryC
[C]
Ok
Processing a 'a' 
ExitC
EntryB
[B]
Ok
Processing a 'a' 
ExitB
ExitA
EntryA
EntryC
[C]
Ok
Processing a 'a' 
ExitC
EntryB
[B]
Ok
Processing a 'a' 
ExitB
ExitA
ExitD
EntryD
EntryA
EntryE
[E]
Ok
Processing a 'a' 
ExitE
EntryB
[B]
Ok
END-OUTPUT
*/
