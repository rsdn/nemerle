using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Console;
using Nemerle.Statechart;
using System.Threading;

  [statechart(<#

  flags testing;
  //flags debug;
  
  name "level conflict with guards and deferrable event";
  
  state D
  {
    
    a [a3] => E;
    a / defer;
    
    state A
    {
      a [a2] => C;
    
      state B
      {
        a [a1] => C;
      }

      state C
      {
        a => B;
      }
      
      state E
      {
        a => B;
      }
    }
  
  }
  #>)]
  public class example
  {
    public a1 : bool {get;set;}
    public a2 : bool {get;set;}
    public a3 : bool {get;set;}
  }

  module Test
  {
    Main() : void
    {
      def fsm = example();
      def tester = fsm.GetTester();
      tester.ConsoleTestInit();
      tester.Initiate("B", "EntryD", "EntryA", "EntryB");
      fsm.a1 = true;
      tester.Test("a", ["B"], ["C"]);
      tester.Test("a", ["C"], ["B"]);
      fsm.a2 = true;
      fsm.a1 = false;
      tester.Test("a", ["B", "A"], ["A", "C"]);
      tester.Test("a", ["C"], ["B"]);
      fsm.a2 = false;
      fsm.a3 = true;
      tester.Test("a", ["B", "A", "D"], ["D", "A", "E"]);
      tester.Test("a", ["E"], ["B"]);
      fsm.a3 = false;
      tester.Test("a", null);
      fsm.a3 = true;
      tester.TestWait("a", 100, "B", "ExitB", "ExitA", "ExitD", "EntryD", "EntryA", "EntryE", "ExitE", "EntryB");
      fsm.Terminate();
    }
  }
  
/*
BEGIN-OUTPUT
EntryD
EntryA
EntryB
[B]
Ok
Processing a 'a' 
ExitB
EntryC
[C]
Ok
Processing a 'a' 
ExitC
EntryB
[B]
Ok
Processing a 'a' 
ExitB
ExitA
EntryA
EntryC
[C]
Ok
Processing a 'a' 
ExitC
EntryB
[B]
Ok
Processing a 'a' 
ExitB
ExitA
ExitD
EntryD
EntryA
EntryE
[E]
Ok
Processing a 'a' 
ExitE
EntryB
[B]
Ok
Processing a 'a' 
[B]
Ok
Processing a 'a' 
ExitB
ExitA
ExitD
EntryD
EntryA
EntryE
ExitE
EntryB
[B]
Ok
END-OUTPUT
*/
