using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{
  
  using StateNode;
  
  enum StateHistory
  {    
    | None
    | Shallow
    | Deep
  }   
     
  [Record]
  variant NameIdentifier
  {
    | Name 
    | QualifiedName {sub_name : string;}
        
    public pos : int;
    public name : string;
    
    public TargetName : string
    {
      get
      {
        match (this)
        {
          | NameIdentifier.Name(name = n)
          | QualifiedName(sub_name = n)  => n
        }
      }
    }
    
    public ParentName : string
    {
      get
      {
        match (this)
        {
          | NameIdentifier.Name  => null
          | QualifiedName(name = n)  => n
        }
            
      }
        
    }
    
    public GetState() : State
    {
      def n = ParentName;
      if (n == null) null else State(n)
    }
        
    public GetNode() : State
    {
      State(TargetName)          
    }
  }
  
  [Record]
  class Actions 
  {   
    public pos : int;
    public actions : list[string];
          
    public this(actions : list[string])
    {
        pos = 0;
        this.actions = actions;
    }
    
    public ActionList : list[string]
    {
        get
        {
            actions
        }
    }
    
    public Empty : bool
    {
      get
      {
        actions.IsEmpty()
      }
    }
     
    public override ToString() : string
    {
      $"$actions"
    }
  }
 
  [Record]
  variant TransitionNode
  {      
    | Event 
        {
          name : string; 
          
          override ToString() : string {name}
        }      
    | Guard 
        {
          condition : string;
          override ToString() : string {Name}
        
          Name : string
          {
            get
            {
              $"Guard$condition"
            }
          }
                            
        }
               
    public pos : int {get;set;}
  }      
  
  [Record]
  variant StateAttribute
  {
    | None
    | Initial 
    | Unknown {name : string}    
    
    [Accessor]
    pos : int
  }
  
  [Flags]
  enum MachineFlag
  {
    | None    
    | Debug                =  0b0001  // enable debug point
    | AutoInitial          =  0b0010  // automatic define initial transition in first defined sub state
    | LocalDefault         =  0b0100  // local transitions by default rather than external
    | TransitionCompleted  =  0b1000  // create TransitionCompleted events
  }
  
  [Record]
  class MachineFlags
  {      
    [FlagAccessor (AutoInitial,  LocalDefault,  Debug, TransitionCompleted,  flags  =  WantSetter)]      
    mutable flags : MachineFlag;
      
      
  }
  
  [Flags]
  enum ActivityFlag
  {
    | None
    | Concurrently = 0b0001 // run several activities concurrently in separate task
    | NoWaitCancel = 0b0010 // no wait of canceling activity
    | NotLong      = 0b0100   // this is not long activity (eg fast action), this implies that parallel task create with specific option, 
                              // prevents long activity in the planner see TaskCreationOptions.LongRunning (this is default for activities)
  }
  
  [Record]
  variant PseudoStateAstNode
  {      
    | Fork {states : list[State];}
    | Join {name : string;target : TargetType;}
    | Junction
    | Choice
    | EntryPoint
    | ExitPoint
    | Terminate 

    public mutable state : State;
      
  }
  
  [Record]
  variant TargetType
  {
    | History 
        {
          history : StateNode.History;
                
          this(pos : int, state : StateNode.State)
          {
            history = StateNode.History(pos, state);
            base(pos);
          }
        }
    | PseudoState
        {
          node : PseudoStateAstNode;                    
          
        }
    | Final {node : StateNode.Final;}
    | State 
        {            
          mutable state : StateNode.State;
                  
        } 
    
    public pos : int;
  }
  
}
