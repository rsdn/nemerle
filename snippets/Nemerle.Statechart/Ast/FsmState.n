using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Diagnostics;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{

  using StateTransition;

  [Record]
  [DebuggerDisplay("State  =  {Name}")]
  partial variant FsmState
  {
    | State  
        {
          public this(st : StateNode.State, reference : bool = false)
          {
            base();
            state = st;      
            this.reference = reference ||  state.reference;
            when (!Reference)
            {
              Name = st.name;
              do_activity = st.do_activity |> StateAction.Do(_.action.ActionList, this, _.flags);
              internal_tran = Internal.Create(this, st.internal_tran);
              entry = st.entry |> StateAction.Entry(_.action.ActionList, this);
              exit = st.exit |> StateAction.Exit(_.action.ActionList, this);
              history = st.history |> StateChartHistory(this, _);
              transitions = Transition.Create(this, st.transitions);
              initial = st.initial |> Initial(this, _);
              def (regs, subst) = st.sub_states.Partition(x => x.regions.HasValue);
              def regs = regs.Map(x => ConcurrentRegion(this, x.regions.Value));              
              super_state = if (st.SuperState != null) State(st.SuperState, true); else null;
              sub_states = (subst |> State(_)) + regs;              
              pseudo_states = st.pseudo_states |> PseudoStateNode.Create(_.node);        
            }
          }
      
        }
    | Final
        {

          public this(super : FsmState, reference : bool =  false)
          {
            base();
            this.super_state = super;
            name =  $"$(super_state)Final";        
            this.reference = reference;
          }

          public override UpdateReference(map : StateMap) : void
          {
            when (super_state.Reference) super_state = map[super_state.name];
          }
        }

    | ConcurrentSubRegion
        {      
          index : int;
          contained_in : ConcurrentRegion;      
      
          public this(regions : ConcurrentRegion, st : StateNode.SubRegion, idx : int)
          {
            contained_in = regions;
            do_activity = st.do_activity |> StateAction.Do(_.action.ActionList, this, _.flags);
            internal_tran = Internal.Create(this, st.internal_tran);
            entry = st.entry |> StateAction.Entry(_.action.ActionList, this);
            exit = st.exit |> StateAction.Exit(_.action.ActionList, this);
            history = st.history |> StateChartHistory(this, _);
            transitions = Transition.Create(this, st.transitions);
            initial = st.initial |> Initial(this, _);                            
            pseudo_states = st.pseudo_states |> PseudoStateNode.Create(_.node);                    
            sub_states = st.sub_states |> State(_);
            sub_states.Iter(x => x.super_state = this);
            index = idx;
            super_state = regions;
          }
    
          public override UpdateReference(map : StateMap) : void
          { 
            base.UpdateReference(map);
            SubStates.StateIter(x => x.part_of_sub_region = Some(this));
            SubStates.StateIter(x => x.part_of_region = Some(contained_in));
          }
      
        }
    
    | ConcurrentRegion
        {
          sub_regions : list[ConcurrentSubRegion] = [];    
    
          mutable exit_pattern : Map[string, list[ConcurrentSubRegion]] = Map();
          mutable enter_pattern : Map[string, list[ConcurrentSubRegion]] = Map();
    
          public this(st : FsmState, node : StateNode.Region)
          {
            name = node.state.name;
            super_state = st;
            transitions = Transition.Create(this, node.transitions);
            sub_regions = node.regions.MapIndex((x, i) => ConcurrentSubRegion(this, x, i));            
            sub_states = sub_regions |> _ : FsmState;
          }

          public static get_pattern(reg : list[ConcurrentSubRegion]) : string
          {
            def idx = (reg |> index).Sort(_ - _);
            def idx = idx |> _.ToString();
            string.Join("", idx)
          }
          
          public DefineExitPattern(reg : list[ConcurrentSubRegion]) : void
          {
            def str = get_pattern(reg);
            when (!exit_pattern.Contains(str)) exit_pattern = exit_pattern.Add(str, reg);      
          }
          
          public DefineEnterPattern(reg : list[ConcurrentSubRegion]) : void
          {
            def str = get_pattern(reg);
            when (!enter_pattern.Contains(str)) enter_pattern = enter_pattern.Add(str, reg);      
          }

          public RegionExcept(st : list[ConcurrentSubRegion]) : ConcurrentSubRegion
          {
            match (sub_regions.Exclude(st, _ == _))
            {
              | [sub] => sub
              | head :: _ => head
              | _ => null
            }
          }          
          
          public RegionsExcept(st : FsmState) : list[ConcurrentSubRegion]
          {
            sub_regions.Filter(x => !x.AllSubStates.Contains(st))
          }

          public RegionsExcept(st : ConcurrentSubRegion) : list[ConcurrentSubRegion]
          {
            sub_regions.Exclude([st], _ == _)
          }
                    
          public mutable join_transitions : list[PseudoStateNode.Join * StateTransition] = [];                                  
          
          public JoinCrossed(join : PseudoStateNode.Join) : bool
          {
            def have_other = Transitions.Filter(x => !(x.ImplicitFrom is PseudoStateNode.Join)).Exists(x => x.trigger == join.trigger);
            def have_guard = join.joined_tran.Exists(x => x.guard.HasValue);
            have_other || have_guard
          }
          
          public HaveJoinTransitions(trigger : StateChartEvent) : bool
          {
            join_transitions.Exists((j, _) => j.trigger == trigger)
          }
          /// <summary>
	        /// Indicates that maybe external transitions from some points of orthogonal state out of region not only completion
	        /// </summary>
          public MayTakePlaceForcedTransitions : bool {get;set;}       
          
          /// <summary>
		      /// Indicates that maybe transitions from outer states forking control to this orthogonal region
		      /// </summary>
          public MayForkingFromOut : bool {get;set;}
        }
        
    public mutable super_state_cross_transitions : list[StateTransition] = [];

    public this()
    {
      Id = StateId(this)
    }

    public mutable do_activity : option[StateAction.Do];
      
    [Accessor] mutable sub_states : list[FsmState] = [];
    public mutable history : TwoOption[StateChartHistory] = TwoOption.None();
    public mutable pseudo_states : list[PseudoStateNode] = [];
            
    [Accessor] mutable history_paths : list[string * HistoryPath] = []; // paths from outer history     
    
    mutable state : StateNode.State;
    
    [Accessor] mutable initial : list[Initial] = [];
    public mutable entry : option[StateAction.Entry] = None();
    public mutable exit  : option[StateAction.Exit] = None();
    [Accessor] mutable internal_tran : list[Internal] = [];
    [Accessor] mutable transitions : list[Transition] = [];
    [Accessor] mutable super_state  : FsmState;
    [Accessor] mutable part_of_sub_region : option[ConcurrentSubRegion] = None();
    [Accessor] mutable part_of_region : option[ConcurrentRegion] = None();
    
    public paths : TransitionPaths {get;set;}
    /// <summary>
    /// Indicates what fsm lived in this state and it not only composite for others
    /// </summary>
    public Lived : bool {get;set;}
    /// <summary>
    /// Hierarchy position
    /// </summary>
    public hierarchy_pos : int {get;set;}
    
    public IsTopState : bool
    {
      get
      {
        Name == TopStateName
      }
    }
    
    public static TopStateName  =  "[Top]";

    [Accessor]
    mutable reference : bool;
    private mutable name : string;

    public Analyser : FsmAnalyser {get;set;}

    public Id : StateId;

    public Name : string
    {
      get
      {        
        def get_name() {if (name != null) name else AstName;}
        match (this)
        {
          | Final => $"$(super_state)Final"
          | State => get_name()
          | ConcurrentRegion => get_name()
          | ConcurrentSubRegion(index = idx, contained_in = r) => $"$(r)SubRegion$idx";
        }
      }
      set
      {
        name = value;
      }
    }

    public AstName : string
    {
      get
      {
        state.name
      }
    }

    /// <summary>
	  /// Events for which reacts state
	  /// </summary>
    public events : list[StateChartEvent]
    {
      get
      {
        def evts = transitions |> trigger;
        def evts = evts.Filter(x => !x.IsCompletion);
        evts.RemoveDuplicatesSort(_.CompareTo(_));
      }
    }

    /// <summary>
	  /// Events for which reacts state or nested states
	  /// </summary>    
    public events_nested : list[StateChartEvent]
    {
      get
      {
        def evts = events + (AllLivedSubStates |>< events);
        evts.RemoveDuplicatesSort(_.CompareTo(_));
      }
    }
    
    public override ToString() : string {Name}

    public virtual UpdateReference(map : StateMap) : void
    {
      Analyser = map.Analyser;      
      when (super_state != null)
        when (super_state.Reference)
        {
          when (super_state.AstName == null) super_state.Name = TopStateName;
          super_state = map[super_state.Name];
        }
      initial.Iter(_.UpdateReference(map));
      transitions.Iter(_.UpdateReference(map));
      history.Iter(_.UpdateReference(map));
      pseudo_states.Iter(_.UpdateReference(map));
    }

    public DefineInitial(st : FsmState) : void
    {
      initial ::= Initial(this, st);
    }

    public AddSubState(st : FsmState) : void
    {
      sub_states = sub_states + [st];
    }

    public CheckAddFinalState() : void
    {            
      match (sub_states.Find(x  => x.this_transitions.Exists(x  => x.To is TransitionTarget.Final)))
      {        
        | Some => AddSubState(Final(this))
        | _ => ()
      }
    }
    
    public DefineTransition(tr : StateTransition) : void
    {
      | Transition as tr => transitions ::= Transition(this, tr)
      | Internal as tr => internal_tran ::= Internal(this, tr)
      | Initial as tr => initial ::= tr
      | _ => ()
    }

    public DefineTransitions(tr : list[StateTransition]) : void
    {
      tr.Iter(DefineTransition);
    }

    public DefineHistoryPath(st : string, nodes : HistoryPath) : void
    {
      history_paths ::= (st, nodes);
    }

    public DefineHistoryPathRecurse(st : string, nodes : HistoryPath) : void
    {
      when (Lived) DefineHistoryPath(st, nodes);
      sub_states.Iter(x  => x.DefineHistoryPathRecurse(st, nodes));
    }

    public DeleteTransitions() : void
    {
      transitions = [];
    }    
    
    public DeleteTransitions(tr : list[StateTransition]) : void
    {
      def (tran, other) = tr.Partition(_ is StateTransition.Transition);
      def (int_tran, other) = other.Partition(_ is StateTransition.Internal);
      def (init, _) = other.Partition(_ is StateTransition.Initial);
      initial = initial.Exclude(init, (x, y) => StateTransition.FullComparer(x, y) == 0);
      internal_tran = internal_tran.Exclude(int_tran, (x, y) => StateTransition.FullComparer(x, y) == 0);
      transitions = transitions.Exclude(tran, (x, y) => StateTransition.FullComparer(x, y) == 0)
    }

    public TraverseTransitions() : void
    {
      paths = TransitionPaths(this);
      paths.TraverseTransitions(true);
    }
    
    public StateIter(iter : FsmState -> void) : void
    {
      iter(this);
      sub_states.Iter(_.StateIter(iter));
    }
        
    public static StateIter(this lst : list[FsmState], iter : FsmState -> void) : void
    {        
      lst.Iter(x => x.StateIter(iter));
    }
    
  }
}
