using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  using PathNode;
  using CheckNode;
  
  public variant GenCtx
  {
    | Normal
    | InEventHandler
    
    public IsInEventHandler : bool
    {
      get
      {
        this is InEventHandler
      }
    }
    public mutable HaveActionsAfter : bool;
  }
  
  class GenElement
  {
    public decl_map : Map[string, string] {get;set;}
    
    public name : string;
    public mutable exec_name : string;
    
    public this(n : string)
    {
      name = n;
    }

    public this(n : string, exec : string, map : Map[string, string])
    {
      name = n;
      decl_map = map;
      exec_name = if (IsDeclared) decl_map.Get(name) else CreateExecName(exec);            
    }
    
    public this(n : string, map : Map[string, string])
    {
      name = n;
      decl_map = map;
      CreateEndExecName();
    }

    public CreateEndExecName() : void
    {
      exec_name = if (IsDeclared) decl_map.Get(name) else CreateExecName(name)
    }

    public static CreateExecName(act : string) : string
    {
      $"On$act"
    }
    
    public IsDestDeclared : bool
    {
      get
      {
        decl_map.Exists((_, x)  => name == x)
      }
    }

    public IsDeclared : bool
    {
      get
      {
        decl_map.Exists((x, _)  => name == x)
      }
    }

    public Declared : string
    {
      get
      {
        if (decl_map.Contains(name)) decl_map.Get(name) else name
      }

    }
    
    public override ToString() : string
    {
      exec_name
    }
    
  }
  
  /// <summary>
  /// Description of GenAction.
  /// </summary>
  class GenAction : GenElement
  {
    [Accessor] node : PathNode;
    
    [Accessor] mutable inlined_actions : list[GenAction] = [];          

    public this(act : string, node : PathNode, map : Map[string, string])
    {
      base(act);
      exec_name = act;
      decl_map = map;
      this.node = node;
      CreateEndName();
      TryInline();
    }

    public this(act : string, exec_name : string, node : PathNode, map : Map[string, string])
    {
      base(act);
      this.exec_name = exec_name;
      decl_map = map;
      this.node = node;
      CreateEndName();
      TryInline();
    }
    
    public action : string
    {
      get
      {
        name
      }
    }
        
    public mutable end_name : string;
    
    public ExecExpr : PExpr
    {
      get
      {
        <[ $(end_name : usesite)(); ]>
      }
    }
    
    public override ToString() : string
    {
      end_name
    }
                
    CreateExecActionName() : string
    {
      if (IsDeclared) Declared else CreateExecName(exec_name)
    }    
         
    public TryInline() : void
    {
      match (node)
      {      
        | Entry(action = act)
        | Exit(action = act) => inlined_actions = create_act_name(act, Action(act), decl_map)
        | _ => ()
      }
    }
        
    public static CreateActions(node : PathNode, map : Map[string, string]) : list[GenAction]
    {
      match (node)
      {
        | Action(act)  => create_act_name(act, node, map)
        | Entry(action = StateAction.Entry(state = st)) => [GenAction($"$(st)Entry", node, map)]
        | Exit(action = StateAction.Exit(state = st)) => [GenAction($"$(st)Exit", node, map)]
        | Sequence(lst) => lst |>< CreateActions(_, map)
        | RunDoActivity as n
        | StopDoActivity as n => [GenAction(n.ToString(), node, map)]
        | _  => []
      }
    }

    public static GetActions(node : PathNode, map : Map[string, string]) : list[GenAction]
    {
      match (node)
      {
        | Action(act)  => create_act_name(act, node, map)
        | Entry(action = StateAction.Entry(state = st) as act) => GenAction($"$(st)Entry", node, map) :: create_act_name(act, Action(act), map)
        | Exit(action = StateAction.Exit(state = st) as act) => GenAction($"$(st)Exit", node, map) :: create_act_name(act, Action(act), map)
        | Sequence(lst) => lst |>< CreateActions(_, map)
        | RunDoActivity as n
        | StopDoActivity as n => [GenAction(n.ToString(), node, map)]
        | _  => []
      }
    }
    
    public CreateEndName() : void
    {
      end_name = match (node)
                 {
                   | Action
                   | Entry 
                   | Exit => CreateExecActionName()
                   | RunDoActivity
                   | StopDoActivity => action
                   | _  => action
                 }
    }        

    static create_act_name(act : Actions, node : PathNode, map : Map[string, string]) : list[GenAction]
    {      
      def names = act.ActionList |> (_, $"$(_)Action");
      names |> GenAction(_, _[2], node, map)
    }

  }
      
  public partial class StaticGenerator
  {
    public static GetExecStateName(st : FsmState, ctx : GenCtx) : PExpr
    {
      def name = $"State$st";
      if (ctx.IsInEventHandler)
        if (st is FsmState.ConcurrentRegion) <[ fsm.$(name : usesite) ]> else <[ $(st.Name : usesite)() ]>
       else <[ $(name : usesite) ]>
    }
    
    public static GetSubRegionsExit(reg : list[FsmState.ConcurrentSubRegion]) : string
    {
      def pat = FsmState.ConcurrentRegion.get_pattern(reg);
      $"Exit$pat"
    }
    
    public static IsEmpty(node : PathNode) : bool
    {
      def implicit = node.Unsequenced.Exists(x => x is Exit || x is Entry);
      node.Actions.IsEmpty() && !implicit
    }    
        
    public static GetHistoryClear(hist : list[StateHistory]) : PExpr
    {
      def expr = hist.Map(x => <[ $($"$(x)Clear" : usesite)(); ]>); 
      <[ {..$expr} ]>
    }
    
    public static get_sub_region_name(r : FsmState.ConcurrentSubRegion) : string
    {
      $"cur_state$(r.index)"
    }

    public GetActions(node : PathNode, map : Map[string, string], supress_inline : bool = false) : PExpr
    {
      def node = node.Unsequenced;
      GetActions(node, map, supress_inline)
    }

    public GetActions(node : list[PathNode], map : Map[string, string], supress_inline : bool = false) : PExpr
    {
      def node = node.Exclude(_ is PathNode.Empty);
      
      def get_act(n)
      {
        | ClearHistory(hist)  => GetHistoryClear(hist)
        | SaveHistory(hist) => if (supress_inline) <[ () ]> else
                               {
                                 match (hist.Filter(x => x.Used))
                                 {
                                   | StateHistory where (SimpleName = n) :: _ => <[ $($"$(n)Save" : usesite)(); ]>
                                   | _ => <[ () ]>
                                 }
                               }
        | RunDoActivity as n
        | StopDoActivity as n => if (supress_inline) <[ () ]> else <[ $($"$n" : usesite)(); ]>;
        | RegionExit([], reg) => def reg_state = GetExecStateName(reg, GenCtx.Normal());
                                 <[ $reg_state.Exit(this) ]>
        | RegionExit(st, reg) => def reg_state = GetExecStateName(reg, GenCtx.Normal());
            def reg_exit = GetSubRegionsExit(st);
            <[ $reg_state.$(reg_exit : usesite)(this) ]>
        | _  => def act =  GenAction.CreateActions(n, map);
            def act = act |> end_name;
            def acts = act |> <[$(_ : usesite)();]>;
                <[ {..$acts} ]>
      }
      
      def body = node |> get_act(_);
      <[ {..$body} ]>
    }
      
    GetForceStateChange(nest : TransitionDestination, ctx : GenCtx) : PExpr
    {
      def st = nest.ConcreteTo;
      def body = GetDestination(nest, ctx);
      match (nest)
      {
        | ForceStateChange(n) => GetForceStateChange(n, ctx)
        | State(FsmState where (PartOfRegion = Some(r), PartOfSubRegion = Some(sr))) =>
            def reg = GetExecStateName(r, ctx);
            def sreg = get_sub_region_name(sr);
            <[ def st = $body;$reg.$(sreg : usesite) = st; st ]>
        | Fork
        | SubRegion
        | Region
        | State => if (st != null) 
                   if (ctx.IsInEventHandler) <[ fsm.SwitchConcrete($body) ]> else
                    <[ SwitchConcrete($body) ]> else 
                    if (ctx.IsInEventHandler) <[ fsm.Switch($body) ]> else <[ Switch($body) ]>
        | Terminate => <[ _ = $body; null]>
        | Join
        | Final(region = FsmState where (HaveFinal = true))
        | TransitionExecute
        | JunctionNode
        | ChoiceNode
        | NoStateChange => body
        | _ => if (st != null)  if (ctx.IsInEventHandler) <[ fsm.SwitchConcrete($body) ]> else
                    <[ SwitchConcrete($body) ]> else 
                    if (ctx.IsInEventHandler) <[ def st = $body;fsm.Switch(st) ]> else <[ def st = $body;Switch(st) ]>
      }
    }
      
  }
}
