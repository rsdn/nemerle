using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of HistoryPath.
  /// </summary>
  public class HistoryPath
  {
  
    public analyser : FsmAnalyser;
    
    public mutable transition : StateTransition;
      
    public this(from : FsmState, to : FsmState, hist : StateHistory, analyser : FsmAnalyser)
    {
      History = hist;  
      FromSource = from;
      ToSource = to;
      this.analyser = analyser
    }
    
    FromSource : FsmState;
    ToSource : FsmState;
    
    public History : StateHistory;
    
    public IsDefault : bool
    {
      get
      {
        transition is StateTransition.Default
      }
    }
    
    public Path : PathNode
    {
      get
      {
        transition.PathNode
      }
    }
    
    public ActualTo : FsmState
    {
      get
      {
        transition.ActualTo
      }
    }

    public From : FsmState
    {
      get
      {
        transition.From
      }
    }
      
    public To : FsmState
    {
      get
      {
        match (transition.To)
          {
            | TransitionTarget.State(st)  => st
            | _  => null
          }
      }
    }

    public Name : string
    {
      get
      {
        if (IsDefault) $"$transition" else 
          match (History)
          {
            | StateHistory where (state = FsmState where (BothHistories = true), Type = t) => $"$(t)$transition"
            | _ => $"$transition"
          }
      }
    }
      
    public override ToString() : string
    {
      Name
    }
    
    public CreatePath() : void
    {
      transition = StateTransition.History(History, FromSource, ToSource);
      transition.Local = true;
      transition.UpdateReference(analyser.Statemap);
      transition.TraversePath(); // creating transition path
      match (History)
      {
          // define history path for transition
        | StateHistory.Shallow  => To.DefineHistoryPathRecurse(FromSource.Name, this)
        | StateHistory.Deep  => To.DefineHistoryPath(FromSource.Name, this);
        | _  => ()
      }
    }
      
  }
}
