using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Statechart;

namespace Nemerle.Statechart
{
  public variant StateType
  {
    | State
    | SubMachine {type : string;}
    | InlinedSubMachine {type : string;}
    | SubRegion
  }
  
  [Record]
  public variant StateNode
  {
    
    | StateMachine 
        {
          flags : MachineFlags;
          state : State;
          
          this(fl : MachineFlags, st : State)
          {
            flags = fl;
            state = st;
            base(st.pos);
          }
          
          GetErrors() : list[Error]
          {
            def get_errors(st, res)
            {
              st.sub_states.FoldLeft(res + st.errors, get_errors)
            }

            get_errors(state, [])
          }
        }
    | Region 
        {
          regions : list[State];
          state : State;
          transitions : list[Transition];
        }
    | Initial 
        {
          guard : option[GuardNode];
          action : option[Actions];
          to : TargetType;
          
          this(pos : int, tran : Transition)
          {
            guard = tran.guard;
            action = tran.action;
            to = tran.to;
            base(pos);
          }
          
          ChangeTarget(t : TargetType) : Initial
          {
            Initial(pos, guard, action, t)
          }
          
        }
    | Default
        {
          action : option[Actions];
          target : StateNode.State;
          history : History;

          Change(pos : int, hist : History) : Default
          {
            Default(pos, action, target, hist)
          }
          
          this(act : option[Actions], to : State)
          {
            action = act;
            target = to;
            base(0)
          }

        }
    | Final
        {
          state  : State;

          this(s : State) 
          {
            state = s;
            base(0);
          }
        }
    | State 
        {
          this (n : string) {name = n;reference = true;base(0);}
          this (n : string, pos : int, type : StateType) 
          {
            base(pos);
            name = n;
            this.type = type;
          }
          this (n : string, pos : int, type : StateType, tran : list[Transition]) 
          {
            base(pos);
            name = n;
            transitions = tran;
            this.type = type;
          }
          this (pos : int, type : StateType) 
          {
            base(pos);
            this.type = type;
          }
          name : string;
          initial : list[Initial] = [];
          attributes : list[StateAttribute];
          entry : list[Entry] = [];
          exit : list[Exit] = [];
          internal_tran : list[InternalTransition] = [];
          transitions : list[Transition] = [];
          do_activity : option[DoActivity] = None();
          history : TwoOption[History] = TwoOption.None();
          sub_states : list[State] = [];
          regions : option[Region] = None();
          [RecordIgnore]
          reference : bool;
          pseudo_states : list[PseudoState] = [];
          type : StateType;
          errors : list[Error] = [];
          
          
        }
    | Entry 
        {
          this(pos : int) {action = Actions([]);base(pos);}
          this(lst : list[string]) {action = Actions(lst);base(0);}
          
          attrib : ActionFlags;
          action : Actions;
        }
    | Exit 
        {
          this(pos : int) {action = Actions([]);base(pos);}
          this(lst : list[string]) {action = Actions(lst);base(0);}
          
          attrib : ActionFlags;
          action : Actions;
        }
    | Error {node : StateNode;}
    | DoActivity {action : Actions;flags : ActivityFlag;}
    | InternalTransition
        {
          evt : list[TransitionNode.Event];
          guard : option[GuardNode];
          action : Actions;
          
          Actions : list[string]
          {
            get
            {
              action.ActionList
            }
          }
          
        }
    | Transition 
        {
          evt : list[TransitionNode.Event];
          guard : option[GuardNode];
          action : option[Actions];
          to : TargetType;

          ChangeTarget(t : TargetType) : Transition
          {
            Transition(pos, evt, guard, action, t)
          }
          
          ToState : StateNode.State
          {
            get
            {
              match (to)
              {
                | TargetType.State(st)  => st
                | _  => null
              }
            }
          }
          
          this(guard : option[GuardNode], action : option[Actions], to : TargetType)
          {
            this.guard = guard;
            this.action = action;
            this.to = to;
            base(to.pos);
          }

          this(evt : List[TransitionNode.Event],  tran : Transition)
          {
            this.evt = evt.NToList();
            guard = tran.guard;
            action = tran.action;
            to = tran.to;
            base(evt.First().pos);
          }
          
        }
            
    | History
        { 
          type : HistoryType;
          default : option[Default];
          state : State;
          reference : bool;
          
          ChangeState(st : State) : History
          {
             History(pos, type, default, st, reference)
          }
          
          ChangeDefault(d : Default) : History
          {
            History(pos, type, Some(d), state, reference)
          }
          
          this(pos : int, state : State)
          {
            this.state = state;
            reference = true;
            default = option.None();
            base(pos);
          }
          
          this(pos : int, type : HistoryType, default : option[Default])
          {
            this.type = type;
            this.default = default;
            base(pos)
          }

        }
    | PseudoState
        {
          node : PseudoStateAstNode;
          
          Change(n : PseudoStateAstNode) : PseudoState
          {
            PseudoState(pos, n)
          }
        }
        
    public pos : int;
       
  }

}
