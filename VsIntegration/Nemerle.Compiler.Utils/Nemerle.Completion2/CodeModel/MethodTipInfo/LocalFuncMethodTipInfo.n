using System;
using System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Compiler;
using Nemerle.Utility;

namespace Nemerle.Completion2
{
  class LocalFuncMethodTipInfo : MethodTipInfo
	{
    _funHeader : Nemerle.Compiler.Typedtree.TFunHeader;

    public this(
      [NotNull] func             : LocalValue,
                nameToken        : Token.Identifier,
                paramsGroupToken : Token.RoundGroup)
    {
      match (func.ValueKind)
      {
        | LocalValue.Kind.Function(funHeader, _) => _funHeader = funHeader;
        | _ => assert(false);
      }
      _ = Init(nameToken, paramsGroupToken);
    }

    CheckIndex(index : int) : void { assert2(index == 0); }

    public override GetCount() : int { 1 }
    public override GetDescription(index : int) : string { CheckIndex(index); "local function" }
    public override GetType(index : int) : string
    {
      CheckIndex(index);
      _funHeader.ReturnType.ToString()
    }

    public override GetParameterCount(index : int) : int
    {
      CheckIndex(index);
      _funHeader.Parameters.Length
    }

    public override GetParameterInfo(index : int, mutable parameter : int) : string * string * string
    {
      CheckIndex(index);
      def parm = _funHeader.Parameters.Nth(parameter);
      //TODO: Add the description extraction for local functions.
      def description = "";
      (parm.Name, $"$(parm.name) : $(parm.ty)", description)
    }

    public override GetName(index : int) : string { CheckIndex(index); _funHeader.Name }
  }
}
