// REFERENCE: Nemerle.Compiler
using Nemerle.Compiler;
using PExpr = Nemerle.Compiler.Parsetree.PExpr;

[Nemerle.MacroUsage (Nemerle.MacroPhase.BeforeTypedMembers,
                     Nemerle.MacroTargets.Assembly)]
macro AssertListedTypeIsInterface(params types : list[PExpr])
{
  def typer = Nemerle.Macros.ImplicitCTX();
  
  foreach(tyAst in types)
  { 
    def bindTy = typer.BindType(tyAst, true, false);
               
    match (bindTy.Hint)
    {
      | Some(ty) =>
        when (!ty.IsInterface && ty.ToString() != "object")
          Message.Error(tyAst.Location, $"Type $ty is not a interface type");
            
      | None => Message.Error(tyAst.Location, "Type inference not allowed here");
    }
  }
}
