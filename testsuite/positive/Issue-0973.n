using System;
using System.IO;
using System.Collections.Generic;

[Record]
public class FileLoader
{
   private _text : string;
   
   [RecordIgnore]
   private mutable _proxies : list[string] = [];

   public Proxies : IEnumerable[string]
   {
     get
     {
       match (_proxies)
       {
         | [] =>
           using (sr = StringReader(_text))
           {
             while (sr.Peek() != - 1)
             {
               def flag =
                 try
                 {
                   _proxies ::= sr.ReadLine();
                   true
                 }
                 catch
                 {
                   _ is FormatException => false
                 }
               when (flag)
                 yield _proxies.Head;
             }
           }
         | _ =>
           foreach(p in _proxies.Reverse())
             yield p;
       }
     }
   }
}

module Program
{
  Main() : void
  {
    def f = FileLoader(<#First line
Second line
Third line
Forth line#>);
    
    foreach (p in f.Proxies)
      Console.WriteLine(p);
      
    foreach (p in f.Proxies)
      Console.WriteLine(p);
  }
}

/*
BEGIN-OUTPUT
First line
Second line
Third line
Forth line
First line
Second line
Third line
Forth line
END-OUTPUT
*/