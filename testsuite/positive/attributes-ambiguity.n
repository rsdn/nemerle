using System;
using System.ComponentModel;
using SCG = System.Collections.Generic;
using System.Console;

[Test(1, 2, 3)]
public class Foo {

  [DefaultValueAttribute("This is string")]
  public Str : string { get; set; }

  [DefaultValue(12)]
  public Int : int { get; set; }

  [DefaultValue(true)]
  public Bool : bool { get; set; }

}

public sealed class TestAttribute : Attribute {

  public this (x : int, params data : array[int]) {
    def l = SCG.List();
    l.Add(x);
    foreach(x in data)
      l.Add(x);
    this.Data = l.ToArray();
  }

  public Data : array[int] { get; set; }

}

module Program {

  Main() : void {
    def get_default_value (prop) {
      def prop_info = typeof(Foo).GetProperty(prop);
      def atb =  Attribute.GetCustomAttribute(prop_info, typeof(DefaultValueAttribute)) :> DefaultValueAttribute;
      atb.Value
    }
    WriteLine(get_default_value("Str"));
    WriteLine(get_default_value("Int"));
    WriteLine(get_default_value("Bool"));

    def test_atb = Attribute.GetCustomAttribute(typeof(Foo), typeof(TestAttribute)) :> TestAttribute;
    foreach(x in test_atb.Data)
      WriteLine(x);
  }

}
/*
BEGIN-OUTPUT
This is string
12
True
1
2
3
END-OUTPUT
*/