//#undef DEBUG_StringTemplate 
//#define DEBUG_StringTemplate 
using System;
using System.Globalization;
using Nemerle.Collections;
using System.Console;
using Nemerle.Utility;
using System.Diagnostics;
using SCG = System.Collections.Generic;

[Record]
public class Index
{
  public Name      : string;
  public Entries   : list[IndexEntry];
}

[Record]
public class IndexEntry
{
  public Name        : string;
  public IsDescend   : bool;
}

public class StgBase
{
  protected ToString(value : double) : string
  {
    value.ToString("### ### ### ##0.00", CultureInfo.InvariantCulture).Trim();
  }
}

#if DEBUG_StringTemplate
[StringTemplate2.StringTemplateGroupDebug]
#else
[StringTemplate.StringTemplateGroup]
#endif
public sealed class MyTemplateGroup : StgBase
{
  Values : list[string] = ["<Test1>", "<Test2>"];
  DoubleValues : list[double] = [123456.78, 987654321.0, 1.0];
  DoubleValue : double = 12345678910.12;
  Value : string = "<Test3>#\n<Test4>#";
   
  IndxEntry(entry : IndexEntry) : string {<#
  $(entry.Name)$(Descend(entry))
  #>}
  
  Batch(indexs : list[Index]) : string {<#
  BEGIN TRAN
    ..$(indexs; "\ngo\n\n"; CreateIndex)
    go
  COMMIT TRAN
  #>}
  
  CreateIndex(index : Index) : string {<#
..CREATE INDEX $(index.Name)
..(
..  ..$(index.Entries; ",\n"; IndxEntry)
..  {
..    $(TestNewLine())
..    ..$(Values; ";\n");
..    $Value
..    ..$DoubleValues
..    ..$(DoubleValues; "\n")
..    $DoubleValue
..  }
..)
..#>}

  static TestNewLine() : string
  {
    def x = "QQQ\nGGG";
    x
  }

  Descend(entry : IndexEntry) : string { if (entry.IsDescend) " DESC" else "" }
  
  public Test1() : void { }
  public Test2() : int { 0 }
} 

module Program
{
  Main() : void
  {
    def indexs = [
      Index("MyIndex1", [IndexEntry("Col1", false), IndexEntry("Col2", true)]),
      Index("MyIndex2", [IndexEntry("Column2", false), IndexEntry("Col3", false)]),
      ];
    System.Console.BackgroundColor = ConsoleColor.DarkCyan;
    def res = MyTemplateGroup().Batch(indexs);
    Write(res);
    System.Console.ResetColor();

    WriteLine();
    Write("...");
    //_ = ReadLine();
  }
}

/*
BEGIN-OUTPUT
BEGIN TRAN
  CREATE INDEX MyIndex1
  (
    Col1,
    Col2 DESC
    {
      QQQ
      GGG
      <Test1>;
      <Test2>;
      <Test3>#
      <Test4>#
      123 456.78, 987 654 321.00, 1.00
      123 456.78
      987 654 321.00
      1.00
      12 345 678 910.12
    }
  )
  go

  CREATE INDEX MyIndex2
  (
    Column2,
    Col3
    {
      QQQ
      GGG
      <Test1>;
      <Test2>;
      <Test3>#
      <Test4>#
      123 456.78, 987 654 321.00, 1.00
      123 456.78
      987 654 321.00
      1.00
      12 345 678 910.12
    }
  )
  go
COMMIT TRAN
...
END-OUTPUT
*/ 