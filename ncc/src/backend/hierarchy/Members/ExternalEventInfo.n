using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using Nemerle.IO;
using Nemerle.Late;
using Nemerle.Utility;
using System.IO;

using DN = dnlib.DotNet;
using PT = Nemerle.Compiler.Parsetree;  
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler 
{
  public class ExternalEventInfo : ExternalMemberInfo, IEvent, IBackendMember, IBackendEvent
  {
   [Accessor]
    protected handle   : DN.EventDef;
    protected adder    : ExternalMethodInfo;
    protected remover  : ExternalMethodInfo;
    protected mem_type : FixedType;

    public this (lib : LibraryReference, h : DN.EventDef)
    {
      handle = h;
      base (lib);

      def tenv = (DeclaringType :> ExternalTypeInfo).tenv;
      adder = CreateMethodInfo(tenv, handle.AddMethod);
      remover = CreateMethodInfo(tenv, handle.RemoveMethod);

      match (adder.GetMemType ()) 
      {
        | FixedType.Fun (t, _) => mem_type = t.FixedValue;
      }
      
      attributes = adder.Attributes;
    }

    public DeclaringType : TypeInfo
    {
      //[Nemerle.Memoize (InvalidValue = null)]
      get { library.ReferenceManager.TypeInfoOfType(handle.DeclaringType) }
    }
    
    public Name       : string      { get { handle.Name } }
    public MemberKind : MemberKinds { get { MemberKinds.Event } }

    public GetAdder()     : IMethod      { adder }
    public GetRemover()   : IMethod      { remover }
    public GetMemType()   : FixedType        { mem_type }

    public CanAccess (_source : TypeInfo) : bool
    {
      // FIXME: this is broken... we need to check method attributes
      true
    }

    GetEventInfo() : DN.EventDef
      implements IBackendMember.GetHandle, IBackendEvent.GetEventInfo
    { handle }
  
    public override ToString () : string { MemberBuilder.DescribeMember (this) }
  } // class
} // namespace
