using Nemerle.Compiler.Backend;
using Nemerle.Compiler.Typedtree;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Utility;

using System.IO;
using System.Diagnostics.SymbolStore;
using System.Diagnostics;
using System.Security;

using DN = dnlib.DotNet;
using NC = Nemerle.Compiler;
using FieldAttributes = dnlib.DotNet.FieldAttributes;

namespace Nemerle.Compiler
{
  public partial class FieldBuilder : IBackendMember, IBackendField
  {
    mutable field_builder : DN.FieldDef;

    GetHandle() : DN.FieldDef
      implements IBackendMember.GetHandle, IBackendField.get_FieldRef
    {
      assert(field_builder != null, Name);
      field_builder
    }

    /**
     * Adds a field builder
     */
    internal override CreateEmitBuilder (tb : DN.TypeDef) : void
    {
      // Converts Nemerle modifiers to the CLI field attributes.
      def make_field_attributes (attrs)
      {
        mutable result = FieldAttributes.PrivateScope;

        when (attrs %&& NemerleModifiers.Public) result |= FieldAttributes.Public;
        when (attrs %&& NemerleModifiers.Private) result |= FieldAttributes.Private;

        if (attrs %&& NemerleModifiers.Internal)
          if (attrs %&& NemerleModifiers.Protected)
            result |= FieldAttributes.FamORAssem;
          else
            result |= FieldAttributes.Assembly
        else
          when (attrs %&& NemerleModifiers.Protected)
            result |= FieldAttributes.Family;

        when (attrs %&& NemerleModifiers.Static) result |= FieldAttributes.Static;
        when (attrs %&& NemerleModifiers.SpecialName) result |= FieldAttributes.SpecialName %|
            FieldAttributes.RTSpecialName;

        unless (attrs %&& (NemerleModifiers.Mutable | NemerleModifiers.CompilerMutable))
          result |= FieldAttributes.InitOnly;

        result
      }

      mutable attrs = make_field_attributes (Attributes);

      when (IsLiteral)
      {
        attrs |= FieldAttributes.Literal | FieldAttributes.HasDefault;
        attrs &= ~FieldAttributes.InitOnly;
      }

      // prevent verification failure
      when (Name != "value__")
        attrs &= ~FieldAttributes.RTSpecialName;

      def fieldType            = GetMemType().ToTypeSig();
      field_builder            = DN.FieldDefUser();
      field_builder.Name       = Name;
      field_builder.FieldType  = fieldType;
      field_builder.FieldSig   = DN.FieldSig(fieldType);
      field_builder.Attributes = attrs;

      when (IsLiteral)
        field_builder.Constant = DN.ConstantUser(const_value.AsObject(InternalType));

      tb.Fields.Add(field_builder);
    }

    internal override Compile () : void
    {
      base.Compile ();
      unless (modifiers.IsEmpty) {
        modifiers.SaveCustomAttributes (declaring_type, fun (target, a, shouldEmit) {
          if (target %&& System.AttributeTargets.Field) {
            when (shouldEmit)
            {
              if (a.TypeFullName == "System.NonSerializedAttribute")
                field_builder.IsNotSerialized = true;
              else
                field_builder.CustomAttributes.Add(a);
            }
            null
          }
          else "field " + ToString ()
        });
      }

      when (IsVolatile) {
        def volatile_attr = AttributeCompilerClass.MakeEmittedAttribute(InternalType.VolatileModifier_tc);
        field_builder.CustomAttributes.Add(volatile_attr)
      }

      when (!IsMutable && !IsLiteral && attributes %&& NemerleModifiers.CompilerMutable)
      {
        def imm_attr = AttributeCompilerClass.MakeEmittedAttribute(InternalType.ImmutableAttribute_tc);
        field_builder.CustomAttributes.Add(imm_attr)
      }
    }
  }
}
