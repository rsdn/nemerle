using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;

using DN = dnlib.DotNet;
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler
{
  internal class TypeSpecCache : SCG.Dictionary[array[DN.TypeSig], GenericTypeSpec]
  {
    new private sealed class Comparer : SCG.IEqualityComparer[array[DN.TypeSig]]
    {
      public static Instance : Comparer = Comparer();

      public GetHashCode(obj : array[DN.TypeSig]) : int
      {
        def comparer = DN.SigComparer(0 :> DN.SigComparerOptions);
        mutable hash = obj.Length;
        foreach (sig in obj)
          unchecked hash ^= comparer.GetHashCode(sig);
        hash
      }

      public Equals(a : array[DN.TypeSig], b : array[DN.TypeSig]) : bool
      {
        | (null, null) => true
        | (null, _) | (_, null) | _ when a.Length != b.Length => false
        | _ =>
          def comparer = DN.SigComparer(0 :> DN.SigComparerOptions);
          for (mutable i = 0; i < a.Length; ++i)
          {
            unless (comparer.Equals(a[i], b[i]))
              return false;
          }
          true
      }
    }

    private _owner : TypeInfo;

    public this(owner : TypeInfo)
    {
      base(Comparer.Instance);
      _owner = owner;
    }

    public GetTypeSpec(specialization : array[DN.TypeSig]) : IBackendType
    {
      mutable typeSpec;
      unless (TryGetValue(specialization, out typeSpec))
      {
        typeSpec = GenericTypeSpec(_owner, specialization);
        Add(specialization, typeSpec);
      }
      typeSpec
    }
  }
} // end ns
