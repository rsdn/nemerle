using Nemerle.Extensions;
using Nemerle.Imperative;
using Nemerle.Utility;

using System;
using System.Runtime;
using System.Runtime.ConstrainedExecution;

namespace Nemerle.Compiler
{
  [Record, StructuralEquality]
  public struct NSpan : IComparable[NSpan]
  {
    public this(pos : int)
    {
      StartPos = pos;
      EndPos = pos;
    }

    public StartPos : int
    {
      get;
    }

    public EndPos   : int
    {
      get;
    }

    public Length : int
    {
      get { EndPos - StartPos }
    }

    public IsEmpty : bool
    {
      get { StartPos == EndPos }
    }

    public static @+(x : NSpan, y : NSpan) : NSpan { NSpan(Math.Min(x.StartPos, y.StartPos), Math.Max(x.EndPos, y.EndPos)) }

    public IntersectsWith(pos : int) : bool { pos <= EndPos && pos >= StartPos }
    public IntersectsWith(start : int, end : int) : bool { start <= EndPos && end >= StartPos }
    public IntersectsWith(other : NSpan)         : bool { IntersectsWith(other.StartPos, other.EndPos) }
    public IntersectsWith(other : Location)       : bool { IntersectsWith(other.StartPos, other.EndPos) }

    public Intersection(start : int, end : int)   : ValueOption[NSpan]
    {
      def start = Math.Max(StartPos, start);
      def end   = Math.Min(EndPos, end);

      if (start <= end)
        VSome(NSpan(start, end))
      else
        VNone()
    }

    public Contains(pos : int) : bool { Contains(pos, pos) }
    public Contains(start : int, end : int) : bool { end < EndPos && start > StartPos }
    public Contains(other : NSpan)         : bool { Contains(other.StartPos, other.EndPos) }
    public Contains(other : Location)       : bool { Contains(other.StartPos, other.EndPos) }

    public override ToString() : string
    {
      $<#"Start: $StartPos Len: $Length"#>;
    }

    public CompareTo(other : NSpan) : int
    {
      def result = StartPos.CompareTo(other.StartPos);
      when (result != 0)
        return result;

      EndPos.CompareTo(other.EndPos)
    }

    public FromStart() : NSpan { NSpan(StartPos) }
    public FromEnd() : NSpan { NSpan(EndPos) }
  }
}
