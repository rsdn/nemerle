/*
 * Copyright (c) 2010 RSDN Team.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using System;
using SCG = System.Collections.Generic;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Assertions;
using Nemerle.Utility;

namespace Nemerle.Compiler
{
  public interface IParser
  {
    Parse(fileIndex : int, code : string) : ParseResult;
  }

  [Record]
  public class ParseResult
  {
    public TopDeclarations : list[TopDeclaration]
    {
      get
    }

    public virtual CommentLocations : SCG.List[Location * string * bool]
    {
      get { SCG.List() }
    }

    public virtual Location : Location
    {
      get { Location.Default }
    }

    public virtual Regions : list[Region]
    {
      get { [] }
    }

    public virtual LinesLengths : array[int]
    {
      get { array(0) }
    }
  }

  [Record]
  public sealed class DefaultParseResult : ParseResult
  {
    private lexer : LexerBase;

    public override CommentLocations  : SCG.List[Location * string * bool]
    {
      get
      {
        match(lexer)
        {
          | lexer is LexerString => lexer.CommentLocations
          | _ => SCG.List()
        }
      }
    }

    public override Location : Location
    {
      get { lexer.Location }
    }

    public override Regions : list[Region]
    {
      get { lexer.Regions }
    }

    public override LinesLengths : array[int]
    {
      get { lexer.LinesLengths }
    }
  }

  [Record]
  public sealed class DefaultParser : IParser
  {
    [Accessor] manager : ManagerClass;

    public Parse(fileIndex : int, code : string) : ParseResult
    {
      def lexer = LexerFile(manager, fileIndex, code);
      when(manager.Options.IndentationSyntax)
        lexer.SwitchToIndentationMode();
      DefaultParseResult(MainParser.Parse(lexer), lexer)
    }

    public static ReadFile([NotNull] filePath : string, checkDuplicatedFiles : bool, checkLastLineForLF : bool) : int * string
    {
      try 
      {
        using(file = IO.FileStream(filePath, IO.FileMode.Open, IO.FileAccess.Read))
        using(stream = IO.StreamReader(file, Text.UTF8Encoding(true, true)))
        {
          def index =
            if(checkDuplicatedFiles)
              Location.AddFile(filePath)
            else
              Location.GetFileIndex(filePath);
          def code = stream.ReadToEnd();
          when(checkLastLineForLF)
          {
            _ = file.Seek (-1 :> Int64, IO.SeekOrigin.End);
            def lastByte = file.ReadByte();
            if (lastByte >= 0)
            {
              def ch = Convert.ToChar(lastByte);
              when (ch != '\n')
                Message.Warning(Location(index, 1, 1), "no new line at the end of the file");
            }
            else
              throw LexerBase.Error("unexpected end of file");
          }
          (index, code)
        }
      }
      catch
      {
        | e => Message.FatalError(Location(filePath, 1, 1, 1, 1), $"cannot open file `$filePath': $(e.Message)")
      }
    }
  }

  public sealed class DispatchingFileParser : IParser
  {
    public this(manager : ManagerClass)
    {
      this(manager, DefaultParser(manager))
    }

    public this(manager : ManagerClass, defaultParser : IParser)
    {
      this.manager = manager;
      this.defaultParser = defaultParser;
    }

    public Register(parserType : Type, fileExtension : string) : void
    {
      registeredParsers[fileExtension.ToLower()] = parserType;
    }

    public Parse(fileIndex : int, code : string) : ParseResult
    {
      def fileName = Location.GetFileName(fileIndex);
      def extension =
        if(!string.IsNullOrEmpty(fileName))
          System.IO.Path.GetExtension(fileName).ToLower()
        else
          "";
      def parser = match(registeredParsers.TryGetValue(extension))
      {
        | (parserType, true) =>
          System.Activator.CreateInstance(parserType, array[manager : object]) :> IParser
        | _ =>
          defaultParser
      }
      parser.Parse(fileIndex, code)
    }

    manager : ManagerClass;
    defaultParser : IParser;
    registeredParsers : Hashtable[string, Type] = Hashtable();
  }
}