/*
 * Copyright (c) 2011 RSDN Team.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using System;
using SCG = System.Collections.Generic;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Assertions;
using Nemerle.Utility;

namespace Nemerle.Compiler
{
  public interface IParser
  {
    Parse(source : ISource) : ParseResult;
  }

  [Record]
  public class ParseResult
  {
    public TopDeclarations : list[TopDeclaration]
    {
      get
    }

    public virtual CommentLocations : SCG.List[Location * string * bool]
    {
      get { SCG.List() }
    }

    public virtual Location : Location
    {
      get { Location.Default }
    }

    public virtual Regions : list[Region]
    {
      get { [] }
    }

    public virtual LinesLengths : array[int]
    {
      get { array(0) }
    }
  }

  [Record]
  public sealed class DefaultParseResult : ParseResult
  {
    private lexer : LexerBase;

    public override CommentLocations  : SCG.List[Location * string * bool]
    {
      get
      {
        match(lexer)
        {
          | lexer is LexerString => lexer.CommentLocations
          | _ => SCG.List()
        }
      }
    }

    public override Location : Location
    {
      get { lexer.Location }
    }

    public override Regions : list[Region]
    {
      get { lexer.Regions }
    }

    public override LinesLengths : array[int]
    {
      get { lexer.LinesLengths }
    }
  }

  [Record]
  public sealed class DefaultParser : IParser
  {
    [Accessor] manager : ManagerClass;

    public Parse(source : ISource) : ParseResult
    {
      def lexer = LexerFile(manager, source.FileIndex, source.GetText());
      when(manager.Options.IndentationSyntax)
        lexer.SwitchToIndentationMode();
      DefaultParseResult(MainParser.Parse(lexer), lexer)
    }
  }

  public sealed class DispatchingFileParser : IParser
  {
    public this(manager : ManagerClass)
    {
      this(manager, DefaultParser(manager))
    }

    public this(manager : ManagerClass, defaultParser : IParser)
    {
      this.manager = manager;
      this.defaultParser = defaultParser;
    }

    public Register(manager : ManagerClass, parserType : Type, fileExtension : string) : void
    {
      def fileExtension = NormalizeExtension(fileExtension);
      if (registeredParsers.ContainsKey(fileExtension))
        Message.Warning("parser for '$fileExtension' files already registered");
      else
      {
        def ctor = parserType.GetConstructor(array[typeof(ManagerClass)]);
        if (ctor != null)
        {
          try
          {
            def parser = ctor.Invoke(array[manager : object]) :> IParser;
            registeredParsers[fileExtension] = Some(parser);
          }
          catch
          {
            | e =>
              Message.Warning($"failed to create instance of '$fileExtension' file parser '$parserType': $(e.Message)");
              registeredParsers[fileExtension] = None();
          }
        }
        else
        {
          Message.Warning($"can't find proper constructor for '$fileExtension' file parser '$parserType'");
          registeredParsers[fileExtension] = None();
        }
      }
    }

    public IsExtensionRegistered(fileExtension : string) : bool
    {
      registeredParsers.ContainsKey(NormalizeExtension(fileExtension))
    }

    public Parse(source : ISource) : ParseResult
    {
      def fileName = Location.GetFileName(source.FileIndex);
      def extension =
        if(!string.IsNullOrEmpty(fileName))
          NormalizeExtension(System.IO.Path.GetExtension(fileName))
        else
          "";
      match (registeredParsers.TryGetValue(extension))
      {
        | (None, true) =>
          Message.Error(Location(source.FileIndex, 1, 1), $"can't parse file with extension '$extension', see parser initialization warnings");
          ParseResult([])
        | (Some(parser), true)
        | _ with parser = defaultParser => parser.Parse(source)
      }
    }

    private static NormalizeExtension(extension : string) : string
    {
      if (string.IsNullOrEmpty(extension))
        ""
      else if (extension[0] == '.')
        extension.Substring(1).ToLowerInvariant()
      else
        extension.ToLowerInvariant()
    }

    manager : ManagerClass;
    defaultParser : IParser;
    registeredParsers : Hashtable[string, option[IParser]] = Hashtable();
  }
}