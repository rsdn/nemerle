/*
 * Copyright (c) 2003-2008 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using Nemerle.Imperative;
using Nemerle.IO;
using Nemerle.Late;
using Nemerle.Utility;
using System;
using System.IO;
using System.Reflection;

using PT = Nemerle.Compiler.Parsetree;
using SR = System.Reflection;
using SRE = System.Reflection.Emit;
using SCG = System.Collections.Generic;
using NC = Nemerle.Compiler;

namespace Nemerle.Compiler
{
  /// LibRefManager for the referenced libraries
  [ManagerAccess]
  public class LibraryReferenceManager
  {
    public NameTree : NamespaceTree;

    internal ReferencedAssemblies : SCG.List[LibraryReference] = SCG.List();

    // a little hack to prevent AddBuildins() calling in ExternalTypeInfo
    internal mutable add_buildins : bool = false;

    // -- CONSTRUCTORS -----------------------------------------------------

    /// constructor. Always load the Framework core library.
    public this (man : ManagerClass, [NotNull] lib_paths : list [string])
    {
      this.NameTree = man.NameTree;
      Manager = man;

      def assembly_dir(t) { DirectoryOfCodebase(getAssemblyLocation(t.Assembly)); }

      // include current directory and directories where
      // mscorlib.dll, System.dll and Nemerle.dll reside
      _lib_path =
           assembly_dir (typeof (Nemerle.Core.AssertionException))
        :: System.Environment.CurrentDirectory
        :: assembly_dir (typeof (System.Text.RegularExpressions.Match))
        :: assembly_dir (typeof (LibraryReferenceManager))
        :: assembly_dir (typeof (System.Object))
        :: lib_paths;
    }


    // -- PUBLIC METHODS ---------------------------------------------------

    /// Stores the referenced library for later lookup by the namespaces. Also
    /// add transitive closure of libraries needed by given assembly.
    public AddLibrary (name : string, isUserReference : bool) : void
    {
      def assembly =
        match (name)
        {
          | "mscorlib" when Manager.Options.UseLoadedCorlib =>
            typeof (System.Object).Assembly

          | "System" when Manager.Options.UseLoadedCorlib =>
            typeof (System.Text.RegularExpressions.Match).Assembly

          | name => LookupAssembly (name)
        };

      if (assembly == null)
      {
        Message.Error ($"cannot find assembly `$name'");
        throw AssemblyFindException()
      }
      else if (_loaded_assemblies_by_name.Contains(assembly.FullName))
      {
        when (_assemblies_loaded_by_hand.Contains(assembly.FullName) && !Manager.Options.PersistentLibraries)
          Message.Warning ($"assembly `$name' already loaded");

        _assemblies_loaded_by_hand[assembly.FullName] = 42;
      }
      else
      {
        _assemblies_loaded_by_hand[assembly.FullName] = 42;
        AddAssembly (assembly, isUserReference)
      }
    }

    public AddAssembly (assembly : SR.Assembly, isUserReference : bool) : void
    {
      unless (_loaded_assemblies_by_name.Contains (assembly.FullName))
      {
        _loaded_assemblies_by_name [assembly.FullName] = LibraryReference (this, assembly, isUserReference); // store the pre-loaded reference

        when (Manager.Options.GreedyReferences)
        {
          def refs = assembly.GetReferencedAssemblies();
          foreach (name : SR.AssemblyName in refs)
          {
            def asm =
              try { assemblyLoad (name) }
              catch
              { _ is FileNotFoundException =>
                try   { assemblyLoadFrom(getAssemblyLocation(name)) }
                catch { _ is System.Exception =>  LookupAssembly(name.Name) }
              };

            when (asm != null)
              AddAssembly (asm, false)
          }
        }
      }
    }

    public GetExtensionMethods(name : string, env : GlobalEnv) : SCG.IEnumerable[IMethod]
    {
      def methods = _extension_methods.GetValueOrDefault(name);

      when (methods != null)
        foreach (method in methods.ToArray())
          when (env.IsOpenNamespace(method.DeclaringType.NamespaceNode.Parent))
            yield method;
    }

    public GetExtensionMethodsForCompletion(pattern : string, env : GlobalEnv) : SCG.IEnumerable[IMethod]
    {
      foreach ((name, methods) in _extension_methods.KeyValuePairs)
        when (Completion.MatchName(name, pattern))
          foreach (method when env.IsOpenNamespace(method.DeclaringType.NamespaceNode.Parent) in methods)
            yield method;
    }

    public GetExtensionMethodsForCompletion(pattern : string) : SCG.IEnumerable[IMethod]
    {
      foreach ((name, methods) in _extension_methods.KeyValuePairs)
        when (Completion.MatchName(name, pattern))
          foreach (method in methods)
            yield method;
    }

    // TODO: Move this method away of this class!
    public AddExtensionMethod (m : IMethod) : void
    {
      def methods = _extension_methods.GetValueOrDefault (m.Name);
      if (methods == null)
      {
        def methods = SCG.List();
        methods.Add(m);
        _extension_methods [m.Name] = methods;
      }
      else methods.Add(m);

      match (m)
      {
        | m is MethodBuilder                       => m.IsExtension = true;
        | m is LibraryReference.ExternalMethodInfo => m.IsExtension = true;
        | m is BuiltinMethod                       => m.IsExtension = true;
        | _ => ();
      }
    }

    /// Remove assembly file name from URL returned by Assembly.CodeBase.
    public static DirectoryOfCodebase (mutable path : string) : string
    {
      path = Path.GetDirectoryName (path);

      // hack for mono returning location in the GAC
      def mono_gac = path.IndexOf ("mono/gac");
      path =
        if (mono_gac != -1)
        {
          if (path.LastIndexOf ("Nemerle") > mono_gac)
            path.Substring (0, mono_gac + 5) + "nemerle/"
          else if (path.LastIndexOf ("/2.0.") > mono_gac)
            path.Substring (0, mono_gac + 5) + "2.0/"
          else
            path.Substring (0, mono_gac + 5) + "1.0/"
        }
        else path;

      path
    }

    protected virtual assemblyLoad (name : string) : SR.Assembly { SR.Assembly.Load (name) }
    protected virtual assemblyLoad (name : SR.AssemblyName) : SR.Assembly { SR.Assembly.Load (name) }

    protected virtual assemblyLoadFrom (path : string) : SR.Assembly
    {
      try { SR.Assembly.LoadFrom (path) }
      catch // Somebody gave us a 32-bit reference on 64-bit system or vice-versa.
      { | _ is System.BadImageFormatException =>
          assemblyLoad (SR.AssemblyName.GetAssemblyName (path).FullName);
      }
    }

    public virtual getAssemblyLocation (assembly : SR.Assembly) : string
    {
      System.Uri(assembly.CodeBase).LocalPath
    }

    public virtual getAssemblyLocation (assembly : SR.AssemblyName) : string
    {
      System.Uri(assembly.CodeBase).LocalPath
    }

    /* -- PRIVATE METHODS -------------------------------------------------- */

    private LookupAssembly (name : string) : SR.Assembly
    {
      def assembly_by_name (path : string)
      {
        try
        {
          def ext = Path.GetExtension(path);

          def path =  match (ext.ToLower ())
                      {
                        | ".dll" | ".exe" => path
                        | _ => path + ".dll"
                      };

          if (File.Exists(path))
            assemblyLoadFrom (path)
          else
            null
        }
        catch
        { | _ is FileNotFoundException => null
          | _ is System.ArgumentException => null
        }
      }

      match (name)
      {
        | x when x.IndexOf ('/') != -1 || x.IndexOf ('\\') != -1 =>
          assembly_by_name (x)

        | x when x.IndexOf (',') != -1 =>
          try { assemblyLoad (x) }
          catch
          { | _ is FileNotFoundException => null
            | _ is System.BadImageFormatException =>
              Message.Error ("passed strong name is invalid or assembly was not found");
              null
          }

        | _ =>
          def lookup(libs)
          {
            | lib :: libs =>
              def ret = assembly_by_name(Path.Combine(lib, name));
              if (ret == null) lookup(libs)
              else ret

            | [] => null
          }

          def assembly = lookup(_lib_path);

          if (assembly == null)
          {
            #pragma warning disable 618 // Obsolete
            Assembly.LoadWithPartialName(name);
            #pragma warning restore 618
          }
          else
            assembly
      }
    }

    private load_macro (lib : SR.Assembly, macro_name : string) : void
    {
      def macro_type = lib.GetType (macro_name.Replace ("&", "\\&"));
      if (macro_type == null)
        Message.Warning ($"failed to lookup macro `$(macro_name)' in assembly $(lib.FullName)");
      else
      {
        def ctor = macro_type.GetConstructor (System.Type.EmptyTypes);
        if (ctor == null)
          Message.Warning ($"failed to lookup constructor in macro `$macro_name' from assembly $(lib.FullName)");
        else match (ctor.Invoke(null))
        {
          | null => Message.Warning ($"failed to construct instance of macro `$macro_name' from assembly $(lib.FullName)");
          | casted_macro is IMacro =>
            def ns = casted_macro.GetNamespace ();
            when (namespace_nodes.Get (ns).IsNone)
              _ = cache_namespace_node (ns);

            Manager.MacrosRegistry.RegisterMacro (casted_macro);

          | obj =>
            def type = obj.GetType().GetInterface("Nemerle.Compiler.IMacro");

            if (type == null)
              Message.Error($"instance of macro `$macro_name' from assembly $(lib.FullName) does not implement IMacro interface");
            else
            {
              def loadedType = typeof(Nemerle.Compiler.IMacro);
              assert2(!type.Equals(loadedType));
              def getAssemblyPath(type : Type) : string
              {
                Uri(type.Assembly.CodeBase).LocalPath
              }

              Message.Error($"The macro `$macro_name' implement the IMacro interface from '$(getAssemblyPath(type))' but the compiler use this interface from '$(getAssemblyPath(loadedType))'.");
            }
        }
      }
    }


    internal LoadLibrariesContents() : void
    {
      try
      {
        AppDomain.CurrentDomain.AssemblyResolve += OnAssemblyResolve;

        foreach ((key, lib) when lib != null in _loaded_assemblies_by_name.KeyValuePairs)
        {
          lib.LoadContents();
          _loaded_assemblies_by_name[key] = null;
          when (lib.IsUserReference)
            ReferencedAssemblies.Add(lib);
        }
      }
      finally
      {
        AppDomain.CurrentDomain.AssemblyResolve -= OnAssemblyResolve;
      }
    }

    private OnAssemblyResolve(_ : object, args : ResolveEventArgs) : SR.Assembly
    {
      mutable lib;
      if (_loaded_assemblies_by_name.TryGetValue(args.Name, out lib))
        if (lib != null)
          lib.Assembly
        else
          null
      else
        null
    }

    public LoadPluginsFrom(name : string, strongPart : string = null, loadMacros : bool = true, loadParsers : bool = true) : void
    {
      mutable assembly = LookupAssembly (name);

      when (assembly == null && strongPart != null)
        assembly = assemblyLoad (name + strongPart);
      if (assembly == null)
        Message.Error ("cannot load assembly with macros `" + name + "'")
      else
      {
        when(loadMacros)
          LoadMacrosFrom(assembly);
        when(loadParsers)
          LoadParsersFrom(assembly);
      }
    }

    public LoadMacrosFrom(assembly : SR.Assembly) : void
    {
      assert(assembly != null);

      // if we already loaded the assembly, then macros are also loaded
      // this might happen when referencing assembly, like Nemerle.Macros
      match (_loaded_assemblies_by_name.Get (assembly.FullName))
      {
        | Some (null) => Nemerle.Imperative.Return (); // library was already loaded and processed, together with its macros
        | _ => ()
      }

      def assemblyAttributes = assembly.GetCustomAttributes(false);
      def macroDefs = assemblyAttributes.Filter(t => t.GetType().FullName == "Nemerle.Internal.ContainsMacroAttribute");
      def operDefs  = assemblyAttributes.Filter(t => t.GetType().FullName == "Nemerle.Internal.OperatorAttribute");

      try
      {
        foreach (definition in macroDefs)
          load_macro (assembly, late(definition.Name) :> string);

        foreach (definition in operDefs)
        {
          def node = NameTree.ExactPath(NString.Split(late(definition.Env) :> string, '.'));
          def od = OperatorDefinition(
            node,
            late(definition.Name) :> string,
            late(definition.Left) :> int,
            late(definition.Right) :> int,
            late(definition.IsUnary) :> bool);
          Manager.MacrosRegistry.AddOperatorDefinition(od);
        }
      }
      catch
      { | e is System.TypeLoadException =>
        Message.Error($"cannot load types from macroassembly `$(assembly.FullName)' ($(getAssemblyLocation(assembly))): $(e.Message)")
      }
    }

    public LoadParsersFrom(assembly : SR.Assembly) : void
    {
      assert(assembly != null);

      def assemblyAttributes = assembly.GetCustomAttributes(false);
      def parserDefs = assemblyAttributes.Filter(t => t.GetType().FullName == "Nemerle.Internal.RegisterParserAttribute");

      try
      {
        foreach(definition in parserDefs)
        {
          def parserTypeName = late(definition.Name) :> string;
          def fileExtension = late(definition.Extension) :> string;
          match(assembly.GetType(parserTypeName))
          {
            | null       => Message.Warning($"failed to lookup parser `$parserTypeName' in assembly $(assembly.FullName)");
            | parserType => Manager.Parser.Register(parserType, fileExtension);
          }
        }
      }
      catch
      { | e is System.TypeLoadException =>
        Message.Error($"cannot load parsers from assembly `$(assembly.FullName)' ($(getAssemblyLocation(assembly))): $(e.Message)")
      }
    }

    public IsAssemblyLoaded(assemblyFullName : string) : bool
    {
      _loaded_assemblies_by_name.Contains(assemblyFullName)
    }

    // -- PRIVATE VARIABLES ------------------------------------------------

    /// List of directories we look for assemblies in.
    private _lib_path : list[string];

    // the non-null values are not-yet processed references, which still need to load contents (types and macros)
    private _loaded_assemblies_by_name : Hashtable[string, LibraryReference] = Hashtable(20);
    private _loaded_macro_assemblies   : Hashtable[string, SR.Assembly]      = Hashtable(20);
    private _assemblies_loaded_by_hand : Hashtable[string, int] = Hashtable(20);
    private namespace_nodes : Hashtable[string, NamespaceTree.Node] = Hashtable(300);
    private mutable _construct_right_away : list[NamespaceTree.Node] = [];
    // extension method name -> list of extension methods
    private _extension_methods : Hashtable[string, SCG.List[IMethod]] = Hashtable();

    /// Remove extension methods defined in code (not external).
    public RemoveInternalExtensionMethods() : void
    {
      foreach ((name, lst) when lst.Exists(_ is MethodBuilder) in _extension_methods.KeyValuePairs)
      {
        _ = lst.RemoveAll(_ is MethodBuilder);

        when (lst.Count == 0)
          _ = _extension_methods.Remove (name);
      }
    }


    // -- TYPE CACHE -----

    internal LoadExtensions () : void
    {
      foreach (n in _construct_right_away)
        _ = n.LookupValue ();
      _construct_right_away = [];
    }

    public IsExtension (attrsProvider : System.Reflection.ICustomAttributeProvider) : bool
    {
      SystemTypeCache.ExtensionAttribute != null && attrsProvider.IsDefined (SystemTypeCache.ExtensionAttribute, false) ||
      SystemTypeCache.SQ_ExtensionAttribute != null && attrsProvider.IsDefined (SystemTypeCache.SQ_ExtensionAttribute, false)
    }

    internal LoadTypesFrom (lib : LibraryReference) : void
    {
      def assembly = lib.Assembly;

      try
      {
        def types = if (lib.IsFriend)
          assembly.GetTypes().Filter(t => t.IsPublic || t.IsNotPublic
            || t.IsNestedPublic ||t.IsNestedFamORAssem || t.IsNestedAssembly).ToArray()
        else
          assembly.GetExportedTypes();

        foreach (t in types)
        {
  //        Message.Debug (name);
          def e = ExternalType (t, lib, null);

          def (ns_node, path_to_type) = framework_nesting (t);

          def mainnode = ns_node.Path (path_to_type);

          when (IsExtension (t))
            _construct_right_away ::= mainnode;

          def tinfo_cache =
            // check if we have met such type before
            match (mainnode.Value)
            {
              | NotLoaded(x)      => NamespaceTree.TypeInfoCache.NotLoadedList([e, x]);
              | NotLoadedList(xs) => NamespaceTree.TypeInfoCache.NotLoadedList(e :: xs);
              | _                 => NamespaceTree.TypeInfoCache.NotLoaded(e);
            };

          // assign wrappers for future loading of typecons
          mainnode.Value = tinfo_cache;
        }
      }
      catch { | e => Message.Error ($"Can't load types from '$assembly'. Error: $(e.Message)."); }
    }

    internal static CacheTypeInfo (t : System.Type, tc : NC.TypeInfo, node : NamespaceTree.Node) : void
    {
      match (node.Value)
      {
        | NamespaceTree.TypeInfoCache.NotLoaded (e) =>
          e.tycon = tc;
          assert2(e.system_type.Equals(t));

        | NamespaceTree.TypeInfoCache.NotLoadedList (es) =>
          foreach (e in es)
            when (e.system_type.Equals (t))
            {
              e.tycon = tc;
              break;
            }

        // this happens when we load external protected type in GetInternalType
        // we need to cache to prevent looping
        | NamespaceTree.TypeInfoCache.No =>
          node.Value = NamespaceTree.TypeInfoCache.Cached (tc)

        | NamespaceReference | MacroCall =>
          Util.ice ("wanted to cache in wrong place - value " + t.TypeFullName ())

        | Cached | CachedAmbiguous =>
          Util.ice ("wanted to cache cached value " + t.TypeFullName ())
      }
    }

    internal static GetInternalType (lib : LibraryReference, t : System.Type,
                                     node : NamespaceTree.Node) : NC.TypeInfo
    {
      match (node.Value)
      {
        | NamespaceTree.TypeInfoCache.Cached(tc) => tc
        | NamespaceTree.TypeInfoCache.No => lib.ConstructTypeInfo (t, node);
        | _ => Util.ice ("not loaded internal type... " + t.Assembly.CodeBase + ":" + t.TypeFullName ())
      }
    }

    internal cache_namespace_node (namespc : string) : NamespaceTree.Node
    {
      def nd = NameTree.ExactPath (NString.Split (namespc, array ['.']));
      mutable assigned = nd;
      while (assigned != null)
      {
        when (assigned.Value is NamespaceTree.TypeInfoCache.No)
          assigned.Value = NamespaceTree.TypeInfoCache.NamespaceReference ();

        assigned = assigned.Parent;
      }

      namespace_nodes.Add (namespc, nd);
      nd
    }

    internal framework_nesting (t : System.Type) : NamespaceTree.Node * list [string]
    {
      unless(IsAssemblyLoaded(t.Assembly.FullName))
      {
        AddAssembly(t.Assembly, false);
        LoadLibrariesContents();
      }

      def loop (t : System.Type, acc)
      {
        def dt = t.DeclaringType;

        if (dt != null)
          loop (dt, Util.StripGenericMark (dt.Name) :: acc)
        else
        {
          def namespc = if (t.Namespace != null) t.Namespace else "";

          match (namespace_nodes.Get (namespc))
          {
            | Some (nd) => (nd, acc)
            | None      => (cache_namespace_node (namespc), acc)
          }
        }
      }
      // workaround mono bug #63768
      if (t.IsPointer)
      {
        def find_name (acc, t : System.Type)
        {
          if (t.IsPointer)
            find_name (acc + "*", t.GetElementType ())
          else
            loop (t, [t.Name + acc])
        }

        find_name ("", t)
      }
      else loop (t, [Util.StripGenericMark (t.Name)])
    }
  }
} /* namespace */
