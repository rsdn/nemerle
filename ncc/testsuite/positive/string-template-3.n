//#undef DEBUG_StringTemplate 
//#define DEBUG_StringTemplate 
using System;
using System.Globalization;
using Nemerle.Collections;
using System.Console;
using Nemerle.Utility;
using System.Diagnostics;
using SCG = System.Collections.Generic;

namespace ReportTemplate 
{
#if DEBUG_StringTemplate
[StringTemplate2.StringTemplateGroupDebug()]
#else
[StringTemplate.StringTemplateGroup()]
#endif
  public abstract class BaseHtmlReportTemplate[T]
  {
    public PrintPage(title : string, data : list[T]) : string {<#
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml" >
      <head>
        <title>$title</title>
      </head>
      <body>
        $(PrintBody(data))
      </body>
    </html>
    #>}
    
    public abstract PrintBody(_data : list[T]) : string;// {""}
  }
 
#if DEBUG_StringTemplate
[StringTemplate2.StringTemplateGroupDebug()]
#else
[StringTemplate.StringTemplateGroup()]
#endif
  public sealed class LinkReportTemplate : BaseHtmlReportTemplate[string * string * int * int * int]
  {
    public override PrintBody (data : list[string * string * int * int * int]) : string {<#
    <table class="Report">
      <tr>
         <th>Product</th>
         <th>Series</th>
         <th>Passport</th>
         <th>Certification</th>
         <th>Declaration</th>
      </tr>
      ..$(data; "\n"; PrintData)
    </table>
    #>}
    
    PrintData(product : string, 
              serialNumber : string, 
              registrationCertificateCount : int, 
              certificateOfConformanceCount : int, 
              conformityDeclarationCount : int) : string {<#
    <tr>
      <td>$(product)</td>
      <td>$(serialNumber)</td>
      <td>$(registrationCertificateCount)</td>
      <td>$(certificateOfConformanceCount)</td>
      <td>$(conformityDeclarationCount)</td>
    </tr>
    #>}
  }

  module Program
  {
    Main() : void
    {
      def data = [
        ("pen",    "P-123-321", 5, 3, 4),
        ("pencil", "P-322-813", 2, 1, 1),
        ("Vase",   "V-742-572", 7, 2, 6),
        ];
      System.Console.BackgroundColor = ConsoleColor.DarkCyan;
      
      def res = LinkReportTemplate().PrintPage("Links", data);
      Write(res);
      
      System.Console.ResetColor();

      WriteLine();
      Write("...");
      //_ = ReadLine();
    }
  }
}


/*
BEGIN-OUTPUT
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" >
  <head>
    <title>Links</title>
  </head>
  <body>
    <table class="Report">
      <tr>
         <th>Product</th>
         <th>Series</th>
         <th>Passport</th>
         <th>Certification</th>
         <th>Declaration</th>
      </tr>
      <tr>
        <td>pen</td>
        <td>P-123-321</td>
        <td>5</td>
        <td>3</td>
        <td>4</td>
      </tr>
      <tr>
        <td>pencil</td>
        <td>P-322-813</td>
        <td>2</td>
        <td>1</td>
        <td>1</td>
      </tr>
      <tr>
        <td>Vase</td>
        <td>V-742-572</td>
        <td>7</td>
        <td>2</td>
        <td>6</td>
      </tr>
    </table>
  </body>
</html>
...
END-OUTPUT
*/ 