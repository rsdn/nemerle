// REFERENCE: Nemerle.Compiler
using System;
using System.IO;
using System.Console;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

namespace TraverseExpr2
{
  macro TraverseExpr2Test()
  {
    TraverseExpr2TestHelper.RunTest();
  }
  
  public module TraverseExpr2TestHelper
  {

    public RunTest() : PExpr
    {
      def res_func(res)
      {
        if (res) "OK" else "Fail"
      }
      
      def test((expr, check), res)
      {
        def res1 = res_func(check(Test(expr)));
        def res2 = res_func(check(TestWithContext(expr)));
        res1 :: res2 :: res
      }

      def test_cases = [
                        (<[ Function(_) ]>, (x, y) => x == 1 && y is <[ Function(x) ]>),
                        (<[ Object.Method(_.prop.List, this, _.flags) ]>, (x, y) => x == 1 && y is <[ Object.Method(x.prop.List, this, x.flags) ]>),
                        (<[ Property ]>, (x, y) => x == 1 && y is <[ x.Property ]>),
                        (<[ Property.Value ]>, (x, y) => x == 1 && y is <[ x.Property.Value ]>),
                        (<[ (Property, Prop2) ]>, (x, y) => x == 1 && y is <[ (x.Property, x.Prop2) ]>),
                        (<[ (_, _[2].Property) ]>, (x, y) => x == 2 && y is <[ (x, y.Property) ]>),
                       ];
                  
      def res = test_cases.FoldLeft(["begin"], test).Reverse();

      def str = $<#..$(res; "\n")#>;
      <[ Write($(str : string)); ]>
    }
    
    public GetParam(num : int) : PExpr
    {
      | 2 => <[ y ]>
      | 1
      | _ => <[ x ]>
    }

    public TestWithContext(expr : PExpr) : int * PExpr
    {
      mutable n_params : int = 1;

      def is_first(ctx)
      {
        | TraverseContext.Top
        | Expr(<[ $_.$_ ]>, Top)
        | Expr(<[ (..$_) ]>, Top) => true
        | _ => false
      }

      def loop(ctx, after, expr)
      {

        if (after)
          match (expr)
          {
            | PExpr.Ref(Name where (Id = "_")) => <[ x ]>
            | <[ $(_ : name) ]> when (is_first(ctx)) => <[ x.$(expr.ToString() : usesite) ]>
            | <[ _ ]> => <[ x ]>
            | _ => expr
          } else
          match (expr)
          {
            | <[ $_.$_ ]>
            | <[ (..$_) ]> => ctx.Push();null
            | PExpr.Indexer(<[ _ ]>, [PExpr.Literal(Literal.Integer(val = val))]) with num = val :> int => n_params = Math.Max(n_params, num);<[ $(GetParam(num)) ]>
            | _ => null
          }
      }

      def expr = Macros.TraverseExprWithContext(None(), expr, null, loop);
      (n_params, expr)
    }

    public Test(expr : PExpr) : int * PExpr
    {
      mutable n_params : int = 1;
      mutable is_first : bool = true;

      def loop(ctx, after, expr)
      {
        if (after)
          match (expr)
          {
            | <[ _ ]>
            | PExpr.Ref(Name where (Id = "_")) => <[ x ]>
            | <[ $(_ : name) ]> when (is_first) => <[ x.$(expr.ToString() : usesite) ]>
            | <[ $_.$_ ]> when (ctx == null)
            | <[ (..$_) ]> when (ctx == null) => expr
            | _ => is_first = false;expr
          } else
          match (expr)
          {
            | <[ $(_ : name) ]>
            | <[ $_.$_ ]> when (ctx == null)
            | <[ (..$_) ]> when (ctx == null) => null
            | PExpr.Indexer(<[ _ ]>, [PExpr.Literal(Literal.Integer(val = val))]) with num = val :> int => n_params = Math.Max(n_params, num);<[ $(GetParam(num)) ]>
            | _ => is_first = false;null
          }
      }

      def expr = Macros.TraverseExpr2(None(), expr, null, loop);
      (n_params, expr)
    }
  }
}
