// REFERENCE: System.Core
using System;
using System.Collections.Generic;
using System.Linq;
using System.Console;
using SCG = System.Collections.Generic;
using Nemerle.Extensions;
using Nemerle.Utility;

module Program
{
  Main() : void
  {
    Threading.Thread.CurrentThread.CurrentCulture = Globalization.CultureInfo.InvariantCulture;

    def l = [1, 2, 3, 4, 5];
    def r = l.Where(i=> i%2 ==0);
    WriteLine($"..$r");

    WriteLine("Select overload by parameters count Begin");

    def xs = [(1.2, "a"), (23d, "b")];

    def res1 = xs.Select(fun((o, t)) { (t, o) });
    WriteLine($"..$res1");

    def res1 = xs.Select(fun(o, t) { (t, o) });
    WriteLine($"..$res1");

    WriteLine("Select overload by parameters count End");

    WriteLine("DelayedOverloadResolution Begin");

    def res = Program.Details.Sum(d => d.UnitPrice * (d.Quantity :> double));
    WriteLine(res);
    def res = Program.Details.Sum(d => d.UnitPrice * d.Quantity);
    WriteLine(res);

    def res = Orders.Select(o => (o.OrderID, o.Details.Sum(d => d.UnitPrice * d.Quantity)));
    WriteLine($"..$res");

    def res = Orders.Select((o, i) => (o.OrderID, i, o.Details.Sum(d => d.UnitPrice * d.Quantity)));
    WriteLine($"..$res");

    WriteLine("DelayedOverloadResolution End");
  }

  Detail : Detail = Detail(1, 2, 10.2, 6, "no text");
  Details : array[Detail] = array[Detail, Detail(1, 3, 20.4, 6, "no text")];
  Orders : list[Order] = [Order(1, Details), Order(2, [Detail(2, 4, 123.02, 7, "no text")])];
}

[Record, StructuralEquality, StructuralHashCode]
class Order
{
  [Accessor] _orderID    : int;
  public Details : SCG.IEnumerable[Detail];
}

[Record, StructuralEquality, StructuralHashCode]
class Detail
{
  [Accessor] _orderID    : int;
  [Accessor] _productID  : int;
  [Accessor] _unitPrice  : double;
  [Accessor] _quantity   : int;
  [Accessor] _text       : string;
}

/*
BEGIN-OUTPUT
2, 4
Select overload by parameters count Begin
(a, 1.2), (b, 23)
(0, (1.2, a)), (1, (23, b))
Select overload by parameters count End
DelayedOverloadResolution Begin
183.6
183.6
(1, 183.6), (2, 861.14)
(1, 0, 183.6), (2, 1, 861.14)
DelayedOverloadResolution End
END-OUTPUT
*/
