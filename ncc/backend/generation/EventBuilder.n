using Nemerle.Compiler.Backend;
using Nemerle.Compiler.Typedtree;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Utility;

using System.Reflection;
using System.IO;
using System.Diagnostics.SymbolStore;
using System.Diagnostics;
using System.Security;

using DN = dnlib.DotNet;
using NC = Nemerle.Compiler;

namespace Nemerle.Compiler
{
  public partial class EventBuilder
  {
    internal mutable event_builder : DN.EventDef;

    GetHandle() : DN.IMemberDef implements IBackendMember.GetHandle
    {
      assert(event_builder != null, Name);
      event_builder
    }

    /// Adds an event builder
    internal override CreateEmitBuilder(tb : DN.TypeDef) : void
    {
      def ty = this.GetMemType().GetSystemType();
      def eventType =
        if (ty is DN.TypeDefOrRefSig as sig)
          sig.TypeDefOrRef
        else
          Manager.SystemTypeCache.MulticastDelegate;

      event_builder = DN.EventDefUser(Name, eventType);
      tb.Events.Add(event_builder);
    }

    internal override Compile () : void
    {
      base.Compile ();
      unless (modifiers.IsEmpty) {
        modifiers.SaveCustomAttributes (declaring_type, fun (target, a, shouldEmit) {
          if (target %&& System.AttributeTargets.Event) {
            when(shouldEmit)
              event_builder.CustomAttributes.Add(a);
            null
          }
          else "event " + ToString ()
        });
      }

      event_builder.AddMethod = adder.GetMethodBuilder();
      event_builder.RemoveMethod = remover.GetMethodBuilder();
    }
  }
}
